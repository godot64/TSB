; ----------------------------------------- 
;
; Tuned Simons' Basic
;
; by Arndt Dettke, 1986
;
; part 6
;
; patched, adding a new feature to COLOUR
; patched on 13.01.2021 (commands DELAY & SECURE)
; patched on 25.01.2021
; patched on 24.03.2021 (command ERROR plus LIST & MOD)
; patched on 03.04.2021 (commands MEM & NRM)
; patched on 21.04.2021 (command RENUMBER)
; patched on 18.07.2021 (LIST)
; patched on 22.11.2021 (LOCALTAB optimized)
; patched on 29.11.2021 (added hold DIR output on keypress)
; patched on 29.12.2021 (bugfix in INST: forced PRG mode)
; patched on 13.05.2022 (command SCRLD/SV DEF added)
; patched on 16.07.2022 (command DELAY: now OFF works)
; patched on 04.08.2022 (bugfix in LIST)
; patched on 14.08.2022 (command: DISPLAY able to show 16 chars)
; patched on 17.09.2022 (de/tokenizer: added CLS token and keyword)
;
; ----------------------------------------- 

	!ct pet

; ----------------------------------------- part of MERGE
; b75e			recoded
domerge	jsr sbind
	ldy #1
	lda ($2d),y
	beq dom0
	iny
	lda ($2d),y
	sta int
	iny
	lda ($2d),y
	sta int+1
	jsr sstrpz
	ldy #3
-	lda ($2d),y
	sta basinbuf,y
	dey
	bpl -
	ldy #3
-	iny
	lda ($2d),y
	sta basinbuf,y
	bne -
	clc
	iny
	sty $0b
	lda $2d
	sta $5a
	adc $0b
	sta $58
	ldy $2e
	sty $5b
	bcc +
	iny
+	sty $59
	jsr tsbblcopy
	lda $31
	ldy $32
	sta $2d
	sty $2e
	ldy $0b
	dey
-	lda basinbuf,y
	sta ($5f),y
	dey
	bpl -
	bmi domerge

dom0	jmp basromei

; ----------------------------------------- bef DUP
; b7b8			new
befdup	jsr incbasbz
	jsr sgetadr	; get source x
	sty ax
	sta ax+1
	jsr sgetbytc	; get source y
	stx ay
	jsr sgetbytc	; get width
	stx spaltenanz
	jsr sgetbytc	; get height
	stx zeilenanz
	jsr getxyf	; get target x, y and mode ($f7)
	jsr sgetbytc	; get zoom factor
	txa
	bne +		; at least: 1
	inx
+	stx chgr		; set zoom factor
	jsr dupit		; process
	jmp endsmb

dupit	ldy #0
	sty helpvec
	sty helpvec+1
	sty $ab
	sty $ac
	sty $ad
	lda ax		; save source x
	sta $c5a4
	lda ax+1
	sta $c5a5
	lda ex		; save target x
	sta xdiff
	lda ex+1
	sta xdiff+1
	jmp dup1

duploop	inc ay		; inc source y
	bne dup1
dup0	inc ax		; inc source x
	bne dup1
	inc ax+1

dup1	ldy $f7		; save mode
	sty $ab
	ldy #11		; test pixel
	sty $f7
	lda ax
	sta $09
	lda ax+1
	sta $0a
	lda ay
	sta $a4
	jsr punkt
	lda $ab		; restore mode
	sta $f7
	lda status	; test result:
	beq dup2
	lda ex		; pixel set, so set pixel at target
	sta $09
	lda ex+1
	sta $0a
	lda ey
	sta $a4
	jsr punkt
	jsr kerromein

dup2	inc helpvec	; zoom x:
	lda helpvec	; equal to factor?
	cmp chgr
	beq dup3
	jsr incex		; no, inc target x and set 1 more pixel
	bne dup1

dup3	jsr incex		; yes, advance target x
	lda #0		; reset zoom counter
	sta helpvec
	inc $ac		; inc x-counter
	lda $ac
	cmp spaltenanz	; is equal to width?
	bne dup0		; no, inc source x, set pixels

	lda #0		; yes, reset x-counter
	sta $ac
	lda $c5a4		; restore source x
	sta ax
	lda $c5a5
	sta ax+1
	lda xdiff		; restore target x
	sta ex
	lda xdiff+1
	sta ex+1
	inc ey		; inc target y
	inc helpvec+1	; zoom y:
	lda helpvec+1
	cmp chgr		; equal to factor?
	bne dup1		; no, add 1 line

	lda #0		; yes, reset zoom counter
	sta helpvec+1
	inc $ad		; inc y-counter
	lda $ad
	cmp zeilenanz	; is equal to height?
	bne +		; no, loop
	rts		; yes, finished

+	jmp duploop

incex	inc ex
	bne +
	inc ex+1
+	rts

; ----------------------------------------- bef AT
; b8b4			new
befat	jsr ateval
	jmp endsmb

ateval	jsr incbasbz
	jsr sfrmevl
	jsr schkstr
	lda $64
	sta $f7
	lda $65
	sta $f8
	jsr schkcom
	jsr sfrmevl
	jsr schkstr
	ldy #0
-	lda ($f7),y
	sta $f9
	lda ($64),y
	sta ($f7),y
	lda $f9
	sta ($64),y
	iny
	cpy #3
	bne -
	jmp schkklz

; ----------------------------------------- bef D!
; b8ea			new
befdelete	jsr incbasbz
	jsr del1
	beq +
	jmp swarm		; error bad mode

+	jsr sstrpz	; compute start of bline
	bcc del3
	ldy #0
	lda ($5a),y
	tax
	iny
	lda ($5a),y
	sta $5b
	stx $5a
del3	ldy #0
-	lda ($5a),y
	sta ($58),y
	inc $58
	bne +
	inc $59
+	inc $5a
	bne +
	inc $5b
+	lda $2d
	cmp $5a
	bne -
	lda $2e
	cmp $5b
	bne -
	lda $58
	sta $2d
	lda $59
	sta $2e
	jsr sbind
	jmp delend

; ----------------------------------------- 
; b932
del1	lda $2b		; from basic start
	ldx $2c
	sta $58
	stx $59
	sec
	lda $2d		; up to var start
	sbc #2
	sta $5a
	lda $2e
	sbc #0
	sta $5b
	ldx #$ff
	stx int+1
	jsr chrgot
	bcc ++
	cmp #$2d		; "-"?
	beq +
-	cmp #$ab		; token "-"?
	bne del2
+	jsr chrget	; get char
	bcc del4		; char?
	bcs -		; token?
++	jsr sadr2int	; no, digit
	pha
	jsr sstrpz
	lda $5f
	ldx $60
	sta $58
	stx $59
	pla
	beq ++
	ldx #$ff
	stx int+1
	cmp #$2d		; "-"?
	beq +
	cmp #$ab		; token "-"?
	bne del2
+	jsr chrget
	bcs del2
del4	jsr sadr2int
	bne del2
del5	jsr sstrpz
++	lda $5f
	ldx $60
	sta $5a
	stx $5b
	cmp $58
	txa
	sbc $59
	bcc +
	lda #0
del2	rts

+	lda #1
	rts

; ----------------------------------------- part of DO
; b99f
dodo	jsr sfordone	; search for DONE
	lda progz		; points to 1 before token of DONE
	ldx progz+1
	sta $a8
	stx $a9
	lda $3d		; sys CONT address
	ldx $3e
	sta progz
	stx progz+1

-	jsr skipif	; then search for ELSE
	cmp $22
	beq do0		; found
	tya
	clc
	adc #5
	tay
	jsr sbzply
	sec
	lda progz
	sbc $a8
	lda progz+1
	sbc $a9
	bcc -

	lda $a8		; not found
	ldx $a9
	sta progz
	stx progz+1
do9	ldy #0
	sty doflag	; old: jsr dec7a
	jmp ($0308)	; old: jmp clrdoflg

do0	jmp setdoflg

; ----------------------------------------- part of RENUMBER
; b9df
ren2	ldy #0		; find PRG end
	lda ($a8),y	; get line link low
	tax
	iny
	ora ($a8),y	; 0? (prg end)
	beq +
	jmp ren5		; ce70 (loop: next line)
; b9ec
+	jmp ren3		; prg end: lst line num in INT

; ----------------------------------------- 
; b9ef
open14	jsr sclose	; ### shorter!
	lda #1		; open 1,4
	ldx #4
	ldy #$ff
	jsr $ffba
	jsr $ffc0
	ldx #1		; cmd 1
	jmp $ffc9

; ----------------------------------------- bef COPY
; ba03			recoded
befcopy	jsr incbasbz
	lda xmaxlow
	pha
	lda xmaxhigh
	pha
	lda multijn
	pha
	ldx #0
	stx multijn
	inx
	stx xmaxhigh	; set 319
	lda #$3f
	sta xmaxlow

	jsr open14

	lda #0
	sta $f8
cploop	sta $22
	sta $61
	ldx #6
-	lda grprntab-1,x	; init printer
	jsr bsout
	dex
	bne -
	stx $5f
	stx $60
cploop1	lda #160
	sta helpvec
	bne +
--	inc $5f
	bne +
	inc $60
+	lda #7
	sta helpvec+1
	bne +
-	inc $61
+	lda $5f
	sta $09
	lda $60
	sta $0a
	lda $61
	sta $a4
	lda #11		; test
	sta $f7
	jsr punkt
	jsr kerromein
	lda status
	beq +
	cmp #8
	beq +
	lda $f8
	ldy helpvec+1
	ora gbittabs,y
	sta $f8
+	dec helpvec+1
	bne -
	lda $f8
	ora #$80
	jsr bsout
	lda #0
	sta $f8
	lda $22
	sta $61
	dec helpvec
	bne --
	inc $5f
	lda $60
	beq cploop1
	lda $22
	clc
	adc #7
	cmp #$c8		; 200?
	bcc cploop	; not yet

	lda #$0f		; close printer
	jsr bsout
	jsr return
	jsr sclose

	pla		; restore
	sta multijn
	pla
	sta xmaxhigh
	pla
	sta xmaxlow
	jmp endsmb

; ----------------------------------------- 
; bab5
grprntab	!by $08,$35,$31,$10,$0f,$0d

; ----------------------------------------- part of COLOUR
; babb
docolor2	beq colc0		; no, set paper and border to blk

	cmp #$2c		; comma?
	beq colc1		; yes, only ink

	jsr sgetbyt1	; get param1 (in .x)
	stx helpvec
	jsr chrgot	; no more?
	beq +		; yes, only paper

	jsr sgetbytc	; get param2 (in .x)
	lda helpvec
	!by $24
colc0	txa
	sta $d020		; border
+	stx $d021		; paper
	jsr chrgot	; no more?
	beq +		; yes, only border and paper

colc1	jsr sgetbytc	; get param3
	stx $0286		; ink
+	rts

; (41)
; ----------------------------------------- part of DO
; bae4
isnull	sty indoloop	; .y=0, clear DO flag
	sty justif	; clear flag for additional IFs
	dey
-	iny
	lda (progz),y
	cmp null,y
	beq -
	cpy #4		; NULL when .y=4
	jmp whichdo	; or DONE?

; ----------------------------------------- bef REC
; baf8			original
befrec	jsr getxyxyf
	lda ey
	sta $61
	lda ex
	sta $5f
	lda ex+1
	sta $60
	lda #0
	sta ey+1
	clc
	lda ax
	adc $5f
	sta ex
	lda ax+1
	adc $60
	sta ex+1
	lda ay
	sta ey
	jsr linzei
	clc
	lda ey
	sta ay
	adc $61
	sta ey
	lda ex
	sta ax
	lda ex+1
	sta ax+1
	jsr linzei
	sec
	lda ax
	sbc $5f
	sta ex
	lda ax+1
	sbc $60
	sta ex+1
	lda ey
	sta ay
	jsr linzei
	sec
	lda ey
	sbc $61
	sta ey
	lda ex
	sta ax
	lda ex+1
	sta ax+1
	jsr linzei
	jmp endsmb

; ----------------------------------------- part of BLOCK
; bb79
mblock	lda ex		; multi BLOCK
	cmp #$9f		; 159?
	bcc +
	lda #$9f
	sta ex
+	lda ey
	cmp #$c7		; 199?
	bcc +
	lda #$c7
	sta ey
+	lda $09
	sta $6f
	and #3
	asl
	tax
	jmp mbl0
; bb9c
-	lda $09
	and #3
	asl
	tax
	bne mbl0
	jsr a8plus8
; bba7
mbl0	lda $a4
	and #7
	tay
	jsr kerromaus
	jsr setmulti
	inc $09
	lda $09
	cmp #$a0		; 160?
	bcs +
	cmp $c5aa
	bcc -
	beq -
+	jsr plus320
	cmp $c5a8
	beq mblock
	bcc mblock
	rts

; ----------------------------------------- bef BLOCK
; bbcc			enhanced
befblock	jsr getxyxyf
	jsr blockzei
	jmp endsmb

; ----------------------------------------- 
; bbd5
blockzei	ldx ay
	lda ax
	sta ay
	stx ax
	ldx ay+1
	lda ax+1
	sta ay+1
	stx ax+1
	lda ay+1
	sta $0a
	lda ax+1
	sta $a5
	lda ay
	sta $09
	lda ax
	sta $a4
	lda #$ff		; set flag: don't colorize
	sta gflag
	jsr punkt		; check if x/y-values are plausible
	lda #0
	sta gflag		; reset flag
	lda status
	cmp #8
	bne +
	jmp swarm		; error: bad mode (not plausible)

+	sec
	lda $a8
	sbc $63
	sta $a8
	lda $a9
	sbc #0
	sta $a9
	clc
	lda ay
	sta $09
	lda ay+1
	sta $0a
	lda ax
	sta $a4
	lda $a8
	sta $aa
	lda $a9
	sta $ab
	lda multijn	; in multi?
	cmp #$2c
	bne +
	jmp mblock	; yes
--
+	lda $09
	sta $6f
	and #7
	tax
	lda $0a
	sta $70
	jmp +
-	lda $09
	and #7
	tax
	bne +
	jsr a8plus8
+	lda $a4
	and #7
	tay
	jsr kerromaus
	jsr sethires
	inc $09
	bne +
	inc $0a
+	lda $0a
	beq +
	lda $09
	cmp #$40
	bcs ++
+	lda $0a
	sta vgla+1
	lda $09
	sta vgla
	lda ex
	sta vgle
	lda ex+1
	sta vgle+1
	jsr vglae
	beq -
	bcc -
++	jsr plus320
	cmp #$c8		; 200?
	bcs +
	cmp ey
	beq --
	bcc --
+	rts

; ----------------------------------------- 

a8plus8	clc
	lda $a8
	adc #8
	sta $a8
	lda $a9
	adc #0
	sta $a9
	rts

; ----------------------------------------- 

plus320	inc $a4
	lda $a4
	and #7
	bne +
	clc		; plus 320
	lda $aa
	adc #$40
	sta $aa
	lda $ab
	adc #1
	sta $ab
+	lda $aa
	sta $a8
	lda $ab
	sta $a9
	lda $6f
	sta $09
	lda $70
	sta $0a
	lda $a4
	rts

; ----------------------------------------- part of MEM
; bcdb			new
memplus	dey		; .y=0
	jsr chrget
	lda (progz),y
	ldx #$ff
	cmp #$94		; SAVE?
	bne +
	ldx #0		; set flag
+	cmp #$93		; LOAD?
	bne +
	ldx #1
+	cmp #$87		; READ?
	bne +
	ldx #2

+	cpx #$ff		; Parameter?
	beq params	; ja, Parameter einlesen

	txa		; sonst: Direktbefehl auslösen
	ora #$80
	sta helpvec
	lda $df01
	and #$6c		; Bit 0,1,4,7 aus
	ora helpvec	
	sta $df01
	lda pport
	pha
	ldx helpvc2
	jmp execdma	; to $b5ed (this part by P. Hagemann)

; ----------------------------------------- w/ Parameters
; bd10
params	sty helpvc2	; .y=0
	tax
	jsr chrget	; advance to parameters:
	cpx #$8c		; RESTORE? (set Reload addresses)
	beq param5
	cpx #$9a		; CONT?	(set Increment type)
	beq param6
	cpx #$96		; DEF?	(set all)
	beq param1
	cpx #$c3		; LEN?	(set amount of data)
	beq param1
	cpx #$b0		; OR?	(set address in C64)
	beq param2
	cpx #$b9		; POS?	(set address & bank in REU)
	beq param3u4
	ldx #32		; otherwise: error "Bad Mode",
	jmp ($0300)

; ----------------------------------------- 
; bd33
param1	jsr sgetadr	; LEN: get amount
	sty $df07
	sta $df08

	jsr chrgot	; more parameters?
	beq ready		; no
	jsr schkcom

param2	jsr sgetadr	; OR: get c64 start
	sty $df02
	sta $df03

	jsr chrgot	; more parameters?
	beq ready		; no
	jsr schkcom

param3u4	jsr sgetadr	; POS: get reu start (and bank)
	sty $df04		; get offset
	sta $df05
	jsr sgetbytc	; get bank 
	stx $df06

	jsr chrgot	; more parameters?
	beq ready		; no
	jsr schkcom

param5	jsr sgetbyt1	; RESTORE: get autoinc flag
	jsr cramdma	; get flag for color RAM DMA
	beq +		; if autoinc=0: off
	tya
	ora #$20		; else set ON
	bne ++
+	tya		; set OFF
	and #$df
++	sta $df01

	jsr chrgot	; more parameters?
	beq ready		; no
	jsr schkcom

param6	jsr sgetbyt1	; CONT: get fix reu flag
	txa
	and #3
	clc
	ror		; rotate value to the decent position
	ror
	ror
	sta $df0a
ready	jmp endsmb

; ----------------------------------------- 
; bd97
	!by 0,0,0,0

; ----------------------------------------- part of COLOUR
; bd9b
docolor	ldx #0
	jsr chrget	; anything following?
	jmp docolor2

; ----------------------------------------- 
; bda3
rtrcout	jmp dscbmout

; ----------------------------------------- bef MEM
; bda6			; new/enhanced
befmem	ldy #1		; commands following?
	lda (progz),y
	bpl memnrml
	jmp memplus	; yes, process

; ----------------------------------------- 
; bdaf
memnrml	lda #$cc		; video now at $cc00
	sta video
	jsr clear		; clr/home

	jsr charen0
	lda #$d0		; open char generator
	sta $21
	lda #$e0		; move chars to $e000
	sta $a9
	lda #$00
	sta $20
	sta $a8
	tax
	tay
-	lda ($20),y
	sta ($a8),y
	inc $20
	bne +
	inc $21
	inx
+	jsr inca8a9
	cpx #16		; 16 pages?
	bne -
	jsr charen1

	lda #$94		; VIC bank 3
	sta $dd00
	lda #$3b		; video at $cc00 (lower)
	sta $d018
	jsr repchrget	; save bef vectors and leave
	jmp setbmode	; at a035: set bad mode to PLACE & RENUMBER

; ----------------------------------------- bef DETECT
; bdef			original
befdetect	jsr sgetbytn
	stx detectart
	lda $d01e,x
	sta detecterg
	jmp endsmb

; ----------------------------------------- bef DISPLAY
; bffe			original
befdisplay
	jsr incbasbz
dpl2	ldx #0
	stx $a7
	inx		; first number: 1
	stx $a6
	lda #>keytab	; base address of texts
	sta $21
	lda #<keytab
	sta $20
; 
dpl1	ldy #0
-	lda textkey,y	; out "KEY"
	beq +
	jsr bsout
	iny
	bne -
+	sta $62 		; .a=0 (hi)
	lda $a6		; out number of f-key
	sta $63		; (lo)
	ldx #$90
	sec
	jsr sxflp
	jsr sfacasc	; returns .y=1
	jsr outasc
	jsr komma		; out ","
	jsr quote		; out quote
	dey

-	lda ($20),y	; get text of f-key
	beq nxkey		; is $00? (end of text)
	cmp #$0d		; is cr? (also end of text)
	beq crtxout	; yes, add "+chr$(13)"

	jsr bsout		; otherwise out text
--	iny
	cpy #16
	bcc -
	beq nxkey

crtxout	jsr quote		; is cr: out quote
	lda #$2b		; out "+"
	jsr bsout
	ldx #0
-	lda textchr,x
	beq +
	jsr bsout		; out "chr$(13)"
	inx
	bne -		; uncond. branch

nxkey	jsr quote		; end of text: out quote
+	inc $a6		; count texts
	clc
	lda $20		; add 16 to address
	adc #16
	sta $20
	bcc +
	inc $21
+	jsr return	; out LF
	lda $a6		; loop until 16 texts
	cmp #17
	bne dpl1

	beq no		; finished

; ----------------------------------------- 
; be78
textkey	!tx "key",0
textchr	!tx "chr$(13)",0

; ----------------------------------------- 
; be85 by P. Hagemann
--	rts
-	jmp scrsvrst

scrsvplus	jsr chrget	; DEF follows?	
	cmp #$8c		; Token for RESTORE?
	beq -
	cmp #$96		; Token for DEF?
	bne --

; ----------------------------------------- DEF after SCRLD/SV
; be94 by P. Hagemann	
defscrsv	jsr sgetbytn
	stx helpvec	; Hibyte Ziel nach helpvec
	jsr sgetbytc	; Anzahl Pages in helpvec+1
	stx helpvec+1

	jsr gyf0		; get mode and sa
	ldx helpvec	; 1st par 0? (yes, don't set anything)
	beq no
	ldy helpvec+1
	lda $f7		; sa: in $f7
	ror		; 2 or 3/5 ?
	bcs +		; odd?

	stx vramhi+1	; even: Hibyte bei sa 2
	sty nvrbl+1	; Pages
	jmp ++		; unbed. Sprung

+	stx bmphi+1	; odd: Hibyte bei 3/5
	sty nbmpbl+1	; Pages

++	ldx ey		; Modus: 0,1,2
	beq no		; Modus 0: nix ändern
	lda #$2c		; sonst BIT schreiben
	sta skipcr	; BIT: Colorram übergehen => nur Videoram
	dex		; Modus 1: mit Startadresse
	beq no		; Sonst (Modus >= 2)
	sta skipslo	; BIT: ohne Startadresse
	sta skipshi

no	jmp leave2	; Abschluss

; ------------------------------------------------------------ 
;  tsb detokenizer (from sys: $a717 via $0306/07)
; ------------------------------------------------------------ 
; bed1
slist3	sty helpvc2	; register retten
	stx helpvc2+1
	pha		; laufendes zeichen retten

-	ldy $028d
	cpy #1		; Shift (1) gedrückt?
	beq -		; if Shift: hold
	tya
	and #2
	bne sl1		; if C=: skip delay & page
	tya
	and #4
	beq page

dlay	ldx #$ff		; if CTRL (4): Delay
	bmi +
ml0	iny		; verzögern
	bne ml0
+	inx
	cpx delay		; Faktor erreicht?
	bne ml0		; ja, weiter

page	ldy pageflag	; is ON?
	beq sl1		; no, skip
	ldy pagewert	; bis zu welcher zeile?
	beq sl1		; bei 0: skip
	cpy $d6		; zeile erreicht?
	bcs sl1

	ldy #0		; ja, hold
ml1	lda ($d1),y	; zeilennummer merken
	sta $0244,y
	iny
	cmp #32
	bne ml1

wait	jsr $ffe1		; Hold, sys: stop? (Abbruch)
	beq sl1		; ja, cont.
	jsr $ffe4		; sys: getkey
	cmp #$0d		; <cr>?
	bne wait		; nein, warten

	lda $d4		; $d4, cursorphase retten
	pha
	lda #0
	tay
	sta $d4		; cursor aus
	jsr clear		; clr/home ausgeben
ml2	lda $0244,y	; zeilennummer wieder hinschreiben
	jsr bsout
	iny
	cmp #32
	bne ml2
	pla
	sta $d4		; cursor rekonstruieren

sl1	pla		; aktuelles zeichen holen

sl2	ldx #>tabbef	; zeiger setzen
	stx $21
	ldx #<tabbef
	stx $20
	cmp #$64		; ist TSB token im .a?
	beq listb		; ja, list tsb

back	ldy helpvc2	; nein, flags für weitermachen im ROM-List
	ldx #0
	rts

; ----------------------------------------- 
; bf45
listb	ldx $d4
	bne back

option	lda optflag	; OPTION aktiv?
	beq ml3
	lda #$12		; ja, rvs on
	jsr bsout
ml3	ldy helpvc2
	iny
	lda ($5f),y	; nächstes Zeichen (Token) holen
	bpl +
	eor #$8f		; umwandeln und auf 0..127 begrenzen
+	tax		; Token als zähler
	ldy #0
	cpx #1		; ist HIRES?
	beq ml7		; ja, skip Suche

list1	dex		; bis zum richtigen Befehlswort blättern...
	beq list2
ml5	jsr inc20
	lda ($20),y	; ...überlesen:
	bpl ml5		; normale Zeichen: skip
	bmi list1		; bei geshiftetem Zeichen: nächstes Befehlswort

list2	jsr inc20
ml7	lda ($20),y	; dann richtiges befehlswort ausgeben
	bmi ml8		; (bis zum geshifteten Zeichen)
	jsr bsout
	iny
	bne ml7
ml8	and #$7f		; geshiftetes zeichen ausgeben
	jsr bsout
	jsr rvsoff	; und rvs off (falls OPTION aktiv ist)

	ldy helpvc2	; flags setzen, weiter im ROM
	iny
	ldx #1
lr1	rts

; ----------------------------------------- Part of USE
; bf8a
roundup	inc helpvec	; now: 1
-	ldx $02a6,y	; no, get digits from last to first
	cpx #$30		; is comma or period?
	bcc +++		; yes, skip rounding
	cpx #$39		; digit: is 9?
	bne +
	inc helpvec
	ldx #$2f		; yes, produce 0
+	inx		; increment digit
++	txa
	dec helpvec
	bmi lr1
	sta $02a6,y	; and write back
	beq lr1
+++	cpx #$20		; is char prefix of value?
	beq +		; yes, leave to set leading 1
	dey		; no, check next char
	bpl -
+	jmp userr		; leave to error routine

; ----------------------------------------- 
; bfb0
	!by 0

; ----------------------------------------- Part of MEM
; bfb1
cramdma	ldy $df01		; get autoinc register
	stx helpvc2	; store dma flag (pos/neg)
	txa
	and #1		;  0 or 1?
	rts

; ----------------------------------------- bef ERROR
; bfba	new (code by D. Temme, 64'er 11/1984)
beferror	jsr chrget
	cmp #$b0		; next char is token of OR?
	beq flpmsg
	jmp swarm		; no, error bad mode

flpmsg	jsr chkdrv	; is drive there? 
flpmsg2	lda drive		; and set
	jsr $ffb4		; sys: talk
	lda #$6f
	sta $b9
	jsr $ff96		; sys: send sa after talk
-	jsr $ffa5		; sys: read from IEC
	jsr bsout		; and out
	cmp #$0d		; until cr
	bne -
	jsr $ffab		; sys: send untalk
	jmp bef0

; ----------------------------------------- bef MOD
; bfe3			new
befmod	jsr get2nyb	; get  ink and paper
	jmp setmod	; and colorize

; ----------------------------------------- part of USE
; bfe9
use9	lda #0
usel9	sta helpvec
-	ldx $61
	lda linenum+1,x
	cmp #$2f		; "/"?
	bcc +
	jsr genout
	inc $61
	dec helpvec
	bne -
+	rts

; ----------------------------------------- 
; c000
	!fill 1000,$ad	; graphics colors
	!fill 24		; gap

; ----------------------------------------- 
; c400
procstack	!fill 256		; stack area for exec

; ----------------------------------------- 
; c500
	!fill 256		; sb variables

; ----------------------------------------- 
; c600
	!fill 77		; sb variables
; c64d (keytab):
	!by $50,$41,$47,$45,$32,$33,$0d,$00,$00,$00,$00,$00,$00,$00,$00,$00	; PAGE23+cr
	!by $52,$55,$4e,$3a,$0d,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00	; RUN:+cr
	!by $4c,$49,$53,$54,$20,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00	; LIST
	!by $4c,$4f,$41,$44,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00	; LOAD
	!by $44,$55,$4d,$50,$0d,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00	; DUMP+cr
	!by $50,$4c,$41,$43,$45,$30,$9d,$00,$00,$00,$00,$00,$00,$00,$00,$00	; PLACE0
	!by $45,$52,$52,$4f,$52,$0d,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00	; ERROR+cr
	!by $43,$4f,$4c,$4f,$55,$52,$31,$31,$2c,$31,$32,$2c,$30,$0d,$00,$00	; COLOUR11,12,0+cr
	!by $44,$49,$53,$50,$4c,$41,$59,$0d,$00,$00,$00,$00,$00,$00,$00,$00	; DISPLAY+cr
	!by $44,$49,$52,$22,$24,$22,$0d,$00,$00,$00,$00,$00,$00,$00,$00,$00	; DIR"$"+cr
	!by $46,$49,$ce,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00	; FI+shift n
	!by $44,$49,$53,$4b,$22,$53,$3a,$00,$00,$00,$00,$00,$00,$00,$00,$00	; DISK"S:
	!by $0e,$08,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00	; lock lower case
	!by $53,$43,$52,$4c,$44,$20,$31,$2c,$38,$2c,$33,$2c,$22,$00,$00,$00	; SCRLD,$1,8,3,"
	!by $43,$53,$45,$54,$20,$32,$3a,$00,$00,$00,$00,$00,$00,$00,$00,$00	; CSET,$2:
	!by $44,$4f,$20,$4e,$55,$4c,$4c,$0d,$00,$32,$00,$00,$00,$00,$00,$00	; DO NULL+cr
; c74d
ata	!by $41,$54,$41

; ----------------------------------------- part of $$/%%
; c750
enhexbin	jsr sgetadr
	pla
	tay
	pla
	pla
	cpy #$24		; "$"?
	bne mkbin		; no, make binary

	ldx #0
	lda int+1		; hibyte=0?
	beq +
	jsr mkhex		; no, make hex
+	lda int		; lobyte
	jsr mkhex		; make hex
	lda #0		; transfer and convert to string
	tay
	sta linenum,x
mkstrng	iny
	jsr $b487		; sys make string
	jsr $b6a3		; sys frestr
	jsr $b47d		; sys set string
	jsr $b68c		; sys generate string
	jmp $b4ca		; sys string on stack

; ----------------------------------------- 
; c77f
mkhex	pha		; get upper nibble
	lsr
	lsr
	lsr
	lsr
	ora #$30		; make hex digit
	cmp #$3a
	bcc +
	clc
	adc #7
+	sta linenum,x	; and store
	inx
	pla		; get lower nibble
	and #$0f		; do the same
	ora #$30
	cmp #$3a
	bcc +
	clc
	adc #7
+	sta linenum,x
	inx
	rts

; ----------------------------------------- 
; c7a2
mkbin	ldx #16		; len of result
	stx helpvec
	dex
	lda #$30		; "0"
-	sta linenum,x	; fill
	dex
	bpl -
	inx		; .x=0
	lda int+1		; hibyte=0?
	bne bin0
	lsr helpvec	; yes, halve result
	bne ++
bin0	asl
	bcc +
	inc linenum,x	; upper byte
+	inx
	cpx #8
	bne bin0
++	tax
	lda int
-	asl		; isolate bits
	bcc +
	inc linenum+8,x	; lower byte
+	inx
	cpx #8
	bne -
	sta linenum+8,x	; 0 to end of result
	tay		; .y=0
	lda helpvec
	cmp #8
	beq mkstrng
	tya
	beq mkstrng	; uncond. branch

; ----------------------------------------- 
; c7de
iskomma	jsr schkcom
	jmp dec7a

; ----------------------------------------- 
; c7e4
wanykey	lda $cb		; any key?
	cmp #$40
	bne wanykey	; yes, loop
	lda #1
+	rts

; ----------------------------------------- part of OUT
; c7ed
outx	jsr chrget	; anything following?
	beq +		; no

	jsr sgetbyt1	; yes, number of error
	cpx #$2e		; wrong number?
	bcc +
	ldx #$80		; yes, indicate as wrong
+	txa
	bmi outx0		; and skip
	beq outx0		; also when error is 0

	cmp #$20		; no, 32 or more?
	bcs +
	asl		; no, sys errors
	tax
	lda $a326,x	; sys: addresses of sys errors
	sta $22
	lda $a327,x
	sta $23
	bne ++		; uncond. branch

+	and #$1f		; tsb errors
	asl
	tax
	lda fehlmtab,x	; addresses of tsb errors
	sta $22
	lda fehlmtab+1,x
	sta $23

++	ldy #0
	sty $13
-	lda ($22),y	; out error text
	pha
	and #$7f
	jsr bsout
	iny
	pla
	bpl -
	lda #$69
	ldy #$a3
	jsr $ab1e		; sys out string " error"
outx0	jmp endsmb

; ----------------------------------------- bef LIN
; c83a			new
beflin	jsr chkdrv	; is drive present?
	jsr incbasbz	; yes, you can save
	jsr del1
	beq +
	jsr del5
	beq +
	jmp swarm		; error bad mode

+	lda $58
	ldx $59
	sta $c1		; start address for save
	stx $c2
	ldy #0
	lda ($5a),y
	tax
	iny
	lda ($5a),y
	stx $ae
	sta $af
	stx $fb
	sta $fc
	lda ($ae),y
	sta helpvec+1
	dey
	lda ($ae),y
	sta helpvec
	lda $ae
	ora $af
	bne +
	lda $2d
	ldx $2e
	sta $ae
	stx $af
	tya
	sta helpvec
	sta helpvec+1
	beq ++
+	tya
	sta ($ae),y
	iny
	sta ($ae),y
	clc
	lda $ae
	adc #2
	sta $ae
	bcc ++
	inc $af

++	jsr chrget
	jsr basromei
	jsr $e1d4		; sys get params for load/save
	jsr $f5ea		; sys SAVE

	lda helpvec
	ora helpvec+1
	beq +
	lda helpvec
	ldx helpvec+1
	ldy #0
	sta ($fb),y
	txa
	iny
	sta ($fb),y
+	jmp endsmb

; ----------------------------------------- 
; c8b5
renum0	sta ay+1		; .a: step (hi)
	sec
	lda $a4		; start line minus step
	sbc ay
	sta $20		; nach $20/1
	lda $a5
	sbc ay+1
	sta $21
	jmp ren1		; 9bca (21)

renum1	tay
rnn1	lda $2b		; get start of basic prog
	ldx $2c
	sta $a8
	stx $a9
	rts		; 10

; ----------------------------------------- 
; c8d4
ren4	ldy #0
	jsr rnn1		; basic start to a8/9
	jmp rbnc		; renumber in line

; ----------------------------------------- 

shwerttab	!by $00,$01,$08,$09

; ----------------------------------------- func NRM
; c8e0
fnnrm	jsr getint	; sys check "("
	jsr sgetstr1
	lda #0
	sta $0d
	ldx progz
	ldy progz+1
	stx $71
	sty $72
	ldx $22
	ldy $23
	stx progz
	sty progz+1
	jsr fromnrm	; patch 11.01.20
; c8fe
	ldx #0
	ldy $69		; .y= len
	cpy #8		; 8 (or 16) must be binary
	bcc ++

	cmp #$25		; 8: leading "%"?
	bne +		; no
	jsr chrget	; yes, skip
+	jsr bincon
	ldy $69
	cpy #16		; 16?
	bcc +
	ldx $a8		; yes
	jsr bincon
+	jmp nrm0

; ----------------------------------------- 
; c921
++	cmp #$24		; leading "$" in string?
	bne +		; no
	jsr chrget	; yes, skip "$"
+	jsr hcon0		; and convert
; (22)
nrm0	lda $69
	cmp #8
	bcc +
	ldy $a8
	jsr sxyflp
+	jsr $b7e2		; sys get back progz
	jmp intback	; ")"?

; ----------------------------------------- bef DELAY
; c93c			enhanced
befdelay 	jsr chkonoff
	stx delay
	jmp endsmb

; ----------------------------------------- bef SECURE
; c945			enhanced
befsecure	jsr chkonoff
	lda #$41		; search code for DISAPA
	sta $22
	clc
	lda $2b
	adc #$ff
	sta $20
	lda $2c
	adc #$ff
	sta $21
	ldy #0
	jsr suchcode1
-	lda $20
	bne +
	dec $21
+	dec $20
	lda #0
	sta ($20),y
	jsr simtok
	lda #$3a		; ":"
	sta ($20),y
	bne -

; ----------------------------------------- bef END LOOP
; c973			original
befendloop
	ldy sploop
	cpy #2
	bcs +
err27	ldx #$27		; error "end loop w/o loop"
	jmp ($0300)

+	dey
	lda stackloop,y
	sta progz+1
	lda stackloop-1,y
	sta progz
	jmp bef0

; ----------------------------------------- 
; c98d
stateofdo	ldy doflag	; DO active?
	beq do4		; no, leave
; c992
stofdo2	ldy doflag	; DO active?
	beq do5		; no
	cpy #$d0
	beq do3
	cpy #$47		; ELSE?
	beq do2
do4	rts

do5	tya
	ora indoloop
	beq do6
	lda $61
	beq do1
do3	tya
	tax
	ldy #0
	lda (progz),y	; DO?
	cmp #$22
	bne do4		; no, return
	cpx #$d0		; yes, skip
	beq clrdoflg

setdoflg	ldy #$d0
clrdoflg	sty indoloop
	sty doflag
do6	jsr flinend	; find line end
	jmp endsmb

do1	jmp dodo

do2	jsr basromau
	jmp do8

; ----------------------------------------- 
; c9cb
nrm2	jsr allnrm	; here: returned from TSB.MEM
	jmp bef0

; ----------------------------------------- 
; c9d1
allnrm	lda vecrenum	; vector of bef RENUMBER
	cmp #<befrenum-1	; unchanged? (after MEM)
	beq +		; yes, leave
; c9d8
	lda progz+2	; chrget changed? (DOS 5.1)
	cmp #$4c
	beq nrm3		; yes, go to "tsb.ext" (!changes!)

!if uniprom {		; if for Uniprom CRT:
	ldx #4
	ldy #$cc
	jsr $7f80		; load "tsb.mem" from there
} else {
	ldx #<nammem	; else load "tsb.mem" from disk
	jsr loadext
	bcs iffail	; leave if fail
}

	lda #<befrenum-1	; repair RENUM vector
	sta vecrenum	; 87b4
	lda #>befrenum
	sta vecrenum+1
	lda #<befplace-1	; repair PLACE vector
	sta vecplace	; 86e2
	lda #>befplace
	sta vecplace+1
+	rts

; ----------------------------------------- 
; c9fa
iffail	pla
	pla
iffail1	ldx #24		; row 24
	lda #0		; column 0 (bottom)
	jsr $e570		; sys set cursor
	jsr basromau
	jmp flpmsg2

; ----------------------------------------- bef INST
; ca09			new
befinst	ldx #<nameext	; load "tsb.ext"
	jsr loadext
	bcs iffail1	; leave if fail

	jsr basromau
	ldy #0
	jsr extension	; cc00: init DOS Wedge
	jmp setbmode	; unlink vectors: PLACE/RENUMBER

; ----------------------------------------- 
; ca1f
nrm3	jsr kerromein
	cli
	jmp $ce6c		; repair CHRGET (inside DOS Wedge!)

; ----------------------------------------- 
; ca26
loadext	stx helpvec
	jsr chkdrv	; drive ready?
	ldx helpvec
	ldy #>names
	lda #7		; yes, set 7 chars
	jsr $ffbd		; sys setnam
	lda #1		; LOAD "name",dr,1
	ldx drive		; get tsb drive
	tay
	jsr $ffba		; sys setlfs
	lda #0
	sta $9d		; force prog mode
	jmp $f4a5		; sys LOAD

; ----------------------------------------- 
; 
null	!tx "null"

; ----------------------------------------- 
; ca44
version	!tx "ad86"	; Signature
	!by $0d
	!tx "2.20918"	; Version Number

; ----------------------------------------- 
; ca4f
names
nammem	!tx "tsb.mem"
nameext	!tx "tsb.ext"
namehsg	!tx "tsb.hsg"

; ----------------------------------------- 
; ca64
repchrget	lda progz+2	; chrget changed (by INST)?
	cmp #$4c
	bne +		; no, leave

	jsr kerromein
	ldx #2
-	lda $e3ab,x	; sys repair chrget
	sta progz+2,x
	dex
	bpl -
+	rts

; ----------------------------------------- 
; ca78
g2nsafe	jsr repchrget	; repair chrget
	jmp get2nyb

; ----------------------------------------- 
; ca7e
byte2	cmp #2
	bcc byte1		; max position? if not: continue
	jmp incbasbz	; yes, out
; ca85
byte0	ldx #0		; formerly called from c8fd (nrm)
	stx $a5		; now unsed
	jmp chrgot
; ca8c
fromnrm	ldx #1		; c8fd: jsr fromnrm
	ldy $69		; len of digit string?
	cpy #4		; less than 4?
	bcc +
	dex
+	stx $a5		; set byte counter accordingly
	jsr dec7a
; ca9a
byte1	jsr chrget	; next char: decimal digit?
	bcc ++		; yes, leave byte1 (so CLC)
	pha		; no, is token? (so SEC)
	pla
	bmi +
	jmp ishex		; no, is hex?
+	sec
++	rts

; ----------------------------------------- Part of bef LOCAL
; caa8 (former address ca7f)

localtab	!fill 88

; ----------------------------------------- 
; cb00
	!fill 256		; sb variables

; ----------------------------------------- 
; cc00 extension area
;
; End of part 6
;



