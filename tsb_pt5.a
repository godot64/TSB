; ----------------------------------------- 
;
; Tuned Simons' Basic
;
; by Arndt Dettke, 1986
;
; part 5
;
; patched on 07.03.2020 (command INSERT)
; patched on 13.01.2021 (commands KEY & VOL)
; patched on 25.01.2021 (added parts of FASTCALL)
; patched on 19.03.2021 (bugfix in EXEC)
; patched on 03.04.2021 (command CSET)
; patched on 25.08.2021 (make OPEN independent from drive number)
; patched on 22.11.2021 (command HRDCPY: optimized)
; patched on 07.12.2021 (minor change at INSERT)
; patched on 20.02.2022 (commands SCRLD/SCRSV, by H. Haberl)
; patched on 05.04.2022 (command CENTRE: new option)
; patched on 16.04.2022 (command FETCH: complies with RVS mode)
; patched on 13.05.2022 (command SCRLD/SV DEF added)
; patched on 01.07.2022 (commands TEXT/CSET: optimized)
; patched on 14.08.2022 (command KEY able to keep 16 chars)
; patched on 25.08.2022 (commands MEM..., by P. Hagemann
; patched on 29.08.2022 (all SCR and MEM commands)
; patched on 30.08.2022 (bugfix: crash if KEY 0 was followed by parameter)
; patched on 09.10.2022 (bugfix: possible crash in FETCH)
; 
; ----------------------------------------- 

	!ct pet

; ----------------------------------------- all MOVE befs
; ac5f			recoded
; here: .y contains (token-1)*2
; entry for: MOVE (.y=28), UP (32/34), LEFT (36/38),
; DOWN (40/42), RIGHT (44/46), INV (24).
; Each of these commands gets performed twice, 1st: set chars, 2nd: set colors

movebef	lda progz		; save address of token in basic code
	sta $c1		; for 2nd pass
	lda progz+1
	sta $c2
	lda #0		; init:
	sta helpvec	; - w-marker
	sta helpvec+1	; - displacement offset
	sta $a6		; - pass counter
	beq mve6		; uncond. branch

; --------------------------- 

mve0	ldy $a7		; 2nd pass: reset basic pointer
	lda $c1		; and token number ($a7)
	sta progz
	lda $c2
	sta progz+1

mve6	tya		; mind: .y: (token#-1)*2
	sta $a7
	pha
	jsr getrcwd	; get parameters: row, column, width, height
			; and compute start address (in $20/1 and $a4/5)
	jsr toa8		; save start address to $a8/9 for wrap
	pla
	cmp #32		; command: upb (32)?
	bcc mve5		; no, move (28), INV (24), FCHR (8), FCOL (6), or FILL (12)

; ac8a
	jsr mdistrb	; distribution vector for all commands

mve4	inc $a6		; advance to 2nd pass
	lda $a6
	cmp #2
	bcc mve0
mve4b	jmp endsmb	; after 2nd pass: finished

; ----------------------------------------- befs FCHR, FCOL, FILL, INV
; ac98
mve5	cmp #16		; is INV?
	bcs mve1		; yes, branch

	and #7		; FCHR?
	lsr
	beq mve2
	lsr		; no, FCOL?
	beq mve3

; ----------------------------------------- bef FILL
	jsr mve9		; FILL: get char
	jsr to20		; reset start address, now:

; ----------------------------------------- bef FCOL
mve3	jsr mvea		; address color ram, get color
	beq mve4		; finished

; ----------------------------------------- bef FCHR
mve2	jsr mve9		; get character
	beq mve4		; finished

; --------------------------- 
mvea	inc $a6		; address color ram
	jsr movez
; --------------------------- 
mve9	inc $a6		; advance pass counter
	jsr sgetbytc	; get next parameter (character or color)
	txa		; value in .x
	ldy spaltenanz
	jsr fillmt	; fill buffer with value in .a
	ldx zeilenanz
-	jsr mkline	; write buffer to address in $20/1
	jsr l20pl40	; next row
fillbox	dex		; all rows
	bne -
	rts

; ----------------------------------------- 
; acd2
mve1	and #4		; command: INV?
	beq mve7
	jsr mveb		; no, MOVE
	beq mve4		; finished

; ----------------------------------------- bef INV
; acdb			enhanced
mve7	jsr befinv
	beq mve4b		; finish after 1st pass

; ace0
befinv	ldx #0
--	ldy #0
-	cpy spaltenanz
	beq mve8
	lda ($20),y	; get char
	eor #$80		; set bit 7 (revert)
	sta ($20),y	; store back
	iny
	bne -
mve8	inx
	cpx zeilenanz
	beq +
	jsr l20pl40
	bne --
+	rts

; ----------------------------------------- bef MOVE
; acfe
mveb	jsr sgetbytc	; get target row
	stx zeileanf
	jsr sgetbytc	; get target column
	stx spalteanf
	jsr movez		; compute address
	lda $a8		; set source
	sta $23
	lda $a9
	sta $24
	jmp moveit	; and move

; ----------------------------------------- Command Distributor
; ad18
mdistrb	and #$0f		; reduce token nums
	lsr
	tay		; upb?
	beq befupb
	cmp #1		; upw?
	beq befupw
	cmp #2		; leftw?
	beq befleftw
	cmp #3		; leftb?
	beq befleftb
	cmp #4		; downb?
	beq befdownb
	cmp #5		; downw?
	beq befdownw
	cmp #6		; rightb?
	beq befleftb

; ----------------------------------------- bef RIGHTW (7) / LEFTW (2)
; ad36			enhanced
befleftw	lda #1
	sta helpvec	; set w-marker
	bne mdb0
; ----------------------------------------- bef UPW (1)
; ad3c			enhanced
befupw	lda #1
	sta helpvec	; set w-marker
	bne befupb
; ----------------------------------------- bef RIGHTB (6) / LEFTB (3)
; ad42			enhanced
befleftb	lda #1		; set displacement to 1
	bne mdb0
; ----------------------------------------- bef DOWNB (4)
; ad46			enhanced
befdownb	lda #2		; set down flag
	bne mdb1
; ----------------------------------------- bef DOWNW (5)
; ad4a			enhanced
befdownw	lda #3		; set down flag and w-marker
mdb1	sta helpvec

; ----------------------------------------- bef UPB (0)
; ad4e			enhanced
befupb	lda #40		; set displacement to 40

; ----------------------------------------- 
; ad50
mdb0	sta helpvec+1	; displacement

	cpy #2		; up (0/1)?
	bcs +
	jmp mcontup	; yes

+	cpy #4		; left (2/3)?
	bcc mcontleft	; yes

	cpy #6		; down (4/5)?
	bcc mcontdown	; yes

; ----------------------------------------- RIGHT(B/W)

mcontright
	dec spaltenanz
	lda helpvec	; w-marker (if=1)
	and #1
	beq +
	ldy spaltenanz
	jsr mfrow		; RIGHTW: store rightmost line to buffer
	bne ++
+	ldy zeilenanz
	jsr mspc0		; RIGHTB: store spaces to buffer
++	jsr mminus	; set src to 1 column before (to $23/4)

	ldx #0
--	ldy spaltenanz	; start rightmost
-	lda ($23),y	; get src
	sta ($20),y	; write to dest
	dey		; proceed to the left
	bne -		; until 1 before leftmost
	inx		; next row
	cpx zeilenanz	; until bottom
	beq +
	jsr l20pl40	; next dest row
	jsr l23pl40	; next src row
	jmp --
	iny		; INY unused
+	jmp mfbox		; restore leftmost w/ previous rightmost

; ----------------------------------------- DOWN(B/W)
; ad9a
mcontdown	dec zeilenanz
	ldx zeilenanz
	beq mcup0
	jsr maddhg	; proceed to bottom row
	jsr mminus	; set source (1 row above)
	lda helpvec	; w-marker (if=1)
	and #1
	beq +
	jsr mfline	; DOWNW: store bottom row to buffer
	beq ++
+	ldy spaltenanz	; DOWNB: store spaces to buffer
	jsr mspc0
++	jsr moveit	; move rows
	dex
	jsr mvit0		; move last row (probably useless)
	jmp mkln0		; restore bottom row at top row

; ----------------------------------------- LEFT(B/W)
; adc3
mcontleft	jsr mplus		; set source (1 column in advance)
	lda helpvec	; w-marker (if=1)
	and #1
	beq +
	ldy #0
	jsr mfrow		; LEFTW: store leftmost to buffer
	bne ++
+	ldy zeilenanz	; LEFTB: store spaces to buffer
	jsr mfspace
++	dec spaltenanz
	jsr moveit	; move from left to right
	ldy spaltenanz
	jmp mfbox		; restore rightmost line from buffer

mcup0	inc zeilenanz	; repair DOWN (?)

; ----------------------------------------- UP(B/W)
; ade8
mcontup	jsr mplus		; set source (1 row in advance)
	lda helpvec
	and #1
	beq +
	jsr mfline	; UPW: store topmost to buffer
	beq ++
+	ldy spaltenanz	; UPB: store spaces to buffer
	jsr mfspace
++	jsr moveit	; move topdown
	jmp mkline	; restore bottommost from topmost

; ----------------------------------------- Move bytes
; ae02
moveit	ldx #0
mvit0	ldy #0
-	cpy spaltenanz
	beq +
	lda ($23),y	; from source
	sta ($20),y	; to dest
	iny
	bne -
+	inx
	cpx zeilenanz
	beq ++		; finished
	lda helpvec	; down flag?
	and #2
	bne +
	jsr l20pl40	; no: top down
	jsr l23pl40
	bne mvit0
+	jsr l20mn40	; yes: bottom up
	jsr l23mn40
	jmp mvit0		; loop

; ----------------------------------------- Set Sources
; ae2f
mplus	clc
	lda $20		; 1 (char/row) next
	adc helpvec+1
	sta $23
	lda $21
	adc #0
	sta $24
++	rts

; ae3d
mminus	sec
	lda $20		; 1 (char/row) before
	sbc helpvec+1
	sta $23
	lda $21
	sbc #0
	sta $24
	rts

; ----------------------------------------- Go to bottom
; ae4b
maddhg	jsr l20pl40
	dex
	bne maddhg
	rts

; ----------------------------------------- Set Vectors
; ae52
l20mn40	sec
	lda $20		; dest: 1 row before
	sbc #40
	sta $20
	bcs +
	dec $21
+	rts

; ae5e
l23mn40	sec
	lda $23		; source: 1 row before
	sbc #40
	sta $23
	bcs +
	dec $24
+	rts

; ----------------------------------------- Restore vertically
; ae6a
mfbox	jsr to20
	ldx #0
-	lda movetab,x
	sta ($20),y
	jsr l20pl40
	inx
	cpx zeilenanz
	bne -
	rts

; ----------------------------------------- Restore horizontally
; ae7e
mkln0	jsr to20
mkline	ldy #0
-	lda movetab,y
	sta ($20),y
	iny
	cpy spaltenanz
	bne -
	rts

; ----------------------------------------- Save to Buffer
; ae8d
mfrow	ldx #0
-	lda ($20),y	; vertically
	sta movetab,x
	jsr l20pl40
	inx
	cpx zeilenanz
	bne -
	jmp to20

; aea2
mfline	ldy #0
-	lda ($20),y	; horizontally
	sta movetab,y
	iny
	cpy spaltenanz
	bne -
	rts

; ----------------------------------------- Spaces to Buffer
; aeb0
mfspace	dey		; fill movetab w/
mspc0	lda #$20		; space
fillmt	sta movetab,y
	dey
	bpl fillmt
	rts

; ----------------------------------------- 
; aeba
	!by 0,0,0

; ----------------------------------------- Retrieve Parameters
; aebd
getrcwd	jsr sgetbytn	; get row
	cpx #25		; >24? 
	bcs err32		; yes, bad mode
	stx zeileanf

	jsr sgetbytc	; get column
	cpx #40		; >39?
	bcs err32		; yes, bad mode
	stx spalteanf

	jsr sgetbytc	; get num of columns
	txa
	beq err32		; 0 is error
	stx spaltenanz

	jsr sgetbytc	; get num of rows
	txa
	beq err32		; 0 is error
	stx zeilenanz

; ----------------------------------------- Compute Addresses
; aee3
movez	ldy #0
	sty $20		; init dest
	sty $a5
	lda $a6		; which pass?
	bne +
	lda video		; video (1st pass)
	!by $2c
+	lda #$d8		; or colors (2nd pass)
	sta $21
	lda zeileanf	; compute start address
	sta $a4
	jsr a4mal40
	clc
	lda $a4
	adc $20
	sta $a4
	lda $a5
	adc $21
	sta $a5
	clc
	lda $a4
	adc spalteanf
	sta $20
	lda $a5
	adc #0
	sta $21
	clc
	lda zeileanf
	adc zeilenanz	; beyond limits?
	cmp #26
	bcs err32
	clc
	lda spaltenanz
	adc spalteanf
	cmp #41
	bcs err32
	rts		; no, proceed

; ----------------------------------------- 
; af2f
err32	jmp swarm		; error "bad mode"

; ----------------------------------------- 
; af32
toa8	lda $20		; save start address
	sta $a8
	lda $21
	sta $a9
	rts
; af3b
to20	lda $a8		; restore start address
	sta $20
	lda $a9
	sta $21
	rts

; ----------------------------------------- bef INSERT
; af44			new
befinsert	jsr get1ststr	; get string and len
	cmp #9
	bne jerr14	; yes, error

	pha		; save len
	ldx #0
	stx $a6
	lda #$2c		; comma follows?
	cmp (progz,x)
	bne err32		; no, bad mode

	pla
	tay
	dey		; .y= len
	sty helpvec
	lda $c7		; save RVS flag
	sta helpvec+1
-	lda ($20),y	; read box string
	cmp #$e0		; RVS intended?
	bcc +		; no
	inc $c7		; yes, set flag for RVS
+	cmp #$60		; convert chars to bcode
	bcc +
	and #$7f
	ora #$40
	bne ++
+	and #$3f
++	dec $c7		; rvs?
	bmi +
	ora #$80		; yes
+	sta $02c0,x	; store converted box chars
	lda helpvec+1	; restore RVS flag
	sta $c7
	inx
	dey
	bpl -
	jsr getrcwd	; get position params
	jsr mkbox
	jmp mve3		; out box

; ----------------------------------------- 
; af8d
get1ststr	jsr frominsrt	; get box definition
	sty $21
	stx $20
	lda $69		; len<>9 or even empty?
	beq jerr14
	rts

; ----------------------------------------- 
; af99
jerr14	jmp err14		; yes, error "illegal quantity"

; ----------------------------------------- 
; af9c
mkbox	jsr mkrow
	jsr mkline	; top row
	jsr l20pl40
	jsr mkrow
	ldx zeilenanz
	dex
	bne +
	inc zeilenanz
	bne ++
+	jsr fillbox	; middle rows
++	jsr mkrow
	jmp mkline	; bottom row
; afbc
mkrow	ldy helpvec	; one row
	lda $02c0,y
	tax
	dey
	lda $02c0,y
	pha
	dey
	lda $02c0,y
	dey
	sty helpvec
	ldy spaltenanz
	dey
	bne +
	iny
	inc spaltenanz
+	sta movetab,y
	dey
	pla
	jsr fillmt
	stx movetab
	rts

; ----------------------------------------- part of CALL
; afe4
fillfast	ldx prstack	; stack voll?
	inx
	beq ++		; ja, keine einträge mehr annehmen

	dex		; sonst: eintragen in stack
	lda $20
	sta procstack,x
	inx
	lda $21
	sta procstack,x
	cpx #$ff		; letzter eintrag?
	beq +
	inx		; sonst: zeiger erhöhen
+	stx prstack
++	rts

; -----------------------------------------
; afff
cfreset	lda #0
	sta execflag
	jmp fastcall

; ----------------------------------------- bef FETCH
; b007			recoded
beffetch	jsr get1ststr	; get ctrl string and its len
	sta fetchlen	; store len of ctrl string
	jsr chkfchlen	; get (and check) len
	stx fetchanz	; get len (max 88)
	jsr schkcom
	jsr stestdirm	; check for direct mode
	jsr crsrout	; show cursor
	lda #$2c		; prepare INPUT
	sta $01ff

; ----------------------------------------- 
; b021
fetchit	jsr fgetkey	; gather input chars
	lda $13		; current io device
	beq +		; is 0? (keyboard)

	jsr $ffb7		; no, sys read floppy status
	and #2		; timeout?
	beq +		; no, proceed
; b02f
	jsr sclrch	; yes, clear io
-	jsr srem		; advance to next basic command
--	jmp endsmb	; end of fetch

; ----------------------------------------- 
; b038
+	lda basinbuf	; anything typed?
	bne +
	lda $13		; no, io device is 
	bne fetchit	;   not keyboard
	beq -		;   keyboard

+	jsr sinput1	; yes, perform kernal input
; b046
	ldx helpvec+1	; was comma in string?
	beq --
	ldy #1		; yes, reconstruct comma
	lda ($49),y
	sta helpvec
	iny
	lda ($49),y
	sta helpvec+1
	txa
	tay
	lda #$2c
	sta (helpvec),y

	bne --		; finish fetch

; ----------------------------------------- 
; b05d
jfoa	jmp foutall	; yes, perform input

; ----------------------------------------- 
; b060
fgetkey	ldx #0		; .x=0: start length of input
	stx helpvec	; store
	stx helpvec+1	; store comma flag
; b066
floop	jsr $ffe4		; sys get key
	sta joywert	; merken
	lda joywert
	beq floop		; nothing typed?

	cmp #$0d		; cr?
	beq jfoa
	cmp #$14		; del?
	bne fquote

; ----------------------------------------- key: delete
; b079
fdelete	ldx helpvec	; no input yet?
	beq floop		; yes, loop

	ldx #6
	jsr crsrdel
	dec helpvec
	bpl floop

; ----------------------------------------- 
; b0b6
fquote	lda $d4		; no, quote mode?
	bne fcheck

	ldx #0		; no, check special chars
-	lda fetchtab,x
	cmp joywert
	beq floop		; found one, so skip it
	inx
	cpx #27
	bne -

; ----------------------------------------- check input char
; b099
fcheck	ldy #0
	lda joywert	; get current char
chkloop	ldx helpvec	; get current len
	cmp ($20),y	; is char in ctrl string?
	beq fchrisin	; yes, store

; ----------------------------------------- 
; b0a4
fspecials	tax
	lda ($20),y	; no, check ctrl string:
	cmp #$13		; home? (a..z)
	beq fchkaz
	cmp #$11		; crsr dwn? (a..z,A..Z)
	beq fchkdig
	cmp #$1d		; crsr rgt? (space..0..9..?)
	beq fchkazup

; ----------------------------------------- 
; b0b3
ftcont	lda joywert	; no, reget char
	iny		; advance to next in list
	cpy fetchlen	; until end of ctrl string
	bne chkloop
	beq ftc0		; at end of input: wait for finish

; ----------------------------------------- 
; b9be
fchrisin	jsr bsout		; sys out
	cmp #$3a		; colon?
	beq +
	cmp #$2c		; comma?
	bne ++
	stx helpvec+1	; yes, store position
+	lda #$2e		; hide comma as period
++	sta basinbuf,x	; store in buffer
	inx		; count chars
	stx helpvec
	cpx fetchanz	; input len?
	beq flastchar	; yes, wait for cr
	jsr crsrout	; otherwise: show cursor
ftc0	jmp floop		; and wait for more input

; ----------------------------------------- 
; b9de
flastchar	jsr $ffe4		; sys get key
	sta joywert	; store
	cmp #$0d		; cr?
	beq foutcr	; yes, cr, end now

fend	cmp #$14		; del?
	beq fdelete
	bne flastchar	; no, wait on

; ----------------------------------------- 
; b0ee
foutall	jsr leerzch	; sys out space (switch crsr off)

foutcr	ldx helpvec
	jmp spuf0cr	; out all

; ----------------------------------------- Checks
; b0f6
fchkdig	cpx #$20		; below space?
	bcc notinit
	cpx #$41		; beyond a?
	bcs notinit
	bcc legal		; no, legal
; b100
fchkazup	cpx #$c1		; below A?
	bcc fchkaz
	cpx #$db		; beyond Z?
	bcs notinit
	bcc legal		; no, legal

fchkaz	cpx #$41		; below a?
	bcc notinit
	cpx #$5b		; beyond z?
	bcs notinit
; b112
legal	txa
	ldx helpvec
	jmp fchrisin	; yes, legal
notinit	jmp ftcont	; no, check against next one

; ----------------------------------------- 
; b11b
crsrout	ldx #3		; crsr out
crsrdel	lda $d4
	pha
	lda $c7		; store rvs flag (to helper)
	sta int
	lda #0		; crsr off
	sta $d4
	jsr showcrsr
	pla
	sta $d4
back2call	rts

; ----------------------------------------- 
; b12f
fcursor	!by $9d,$92,$20,$12	; rvs on, space, rvs off, crsr lft
	!by $9d,$9d,$20	; space, crsr lft, crsr lft
; ----------------------------------------- 
; b136
fetchtab	!by $05,$11,$12,$13,$1c,$1d,$1e,$1f	; all these 26 get skipped
	!by $90,$91,$92,$93,$94,$9c,$9d,$9e	; (color codes, crsr, rvs, home)
	!by $9f,$8d,$81,$95,$96,$97,$98,$99
	!by $9a,$9b

; ----------------------------------------- Part of DESIGN
; b157
usedesflg	inx
	lda helpvec
	beq +
	jsr incbasbz
+	cpx #8
	rts

; ----------------------------------------- part of CALL
; b15b
fastcall	ldx prstack	; einträge im labelstack?
	beq back2call	; nein
	inx		; voll?
	beq back2call	; ja

fast	dex		; nein
	jsr incbasbz	; pz auf 1. zeichen

m8	dex
	lda procstack,x	; vektor auf label setzen
	sta $21
	dex
	lda procstack,x
	sta $20
	ldy #$ff		; .y=$ff wg. labelvergleich
	jsr space		; führendes blank überlesen
	jsr compare	; vergleichen von aufruf und label
	beq +
	txa		; alle labels durch?
	bne m8		; nein, weiter
	jmp dec7a		; sonst: pz eins zurück, normale suche:

+	pla
	pla
	jmp found

; ----------------------------------------- part of USE
; b188
use1a	jsr use3		; get USE parameters (b3e2)
	jsr sclrch
	jmp endsmb

; ----------------------------------------- bef SCRLD
; b191	(patch by Hans Haberl), recoded by Peter Hagemann (sa=5)
befscrld	jsr intro
	stx $b5		; store hibyte
	ldx $b8		; file#
	jsr $ffc6		; chkin
	jsr chkstate	; file exists?
	jsr $ffcf		; yes, skip load address
	lda #$cf		; set (ff)cf= basin to distributor
	bne ss1
	
; ----------------------------------------- bef SCRSV
; b1a5
befscrsv	jsr intro
	stx $b5		; Startadr. High merken
	ldx $b8		; file#
	jsr $ffc9		; CHKOUT
	tya		; $00 = Low-Byte Startadresse
skipslo	jsr bsout
	lda $b5		; und High-Byte Startadresse schreiben
skipshi	jsr bsout	
	lda #$d2		; set (ff)d2= bsout to distributor
; b1ba
ss1	sta $f8
ss0	bit $fa		; check sa, V an bei sa= 3 oder 5
	bvc color
; b1c0
nbmpbl	ldx #32		; 3/5: 32 pages (bitmap)
	lda $b5		; Hi-Byte Bitmap ($e0 oder gePOKEt)
	jsr bloop		; Bitmap Subr. an $9efe
	bne ss5		; Status: Fehler?
	bit $fa
	bmi ss5		; nur Bitmap? (wenn sa=3)
; b1cd	
color	lda $f7		; nein, $04 (bei Text), $c0 (bei Grafik) oder gePOKEt
	sta $fc
nvrbl	ldx #4		; Anzahl Pages
; b1d3
col	lda ($fd),y	; Farb-RAM
skipcr	jsr distr		; process
	sta ($fd),y	; and restore
	lda ($fb),y	; Video-RAM
skipvr	jsr distr		; process
	sta ($fb),y	; and restore
	iny
	bne col
	inc $fc
	inc $fe
	dex	    	; count pages
	beq ss5
	lda status	; nach 256 Bytes
	beq col

; ----------------------------------------- 
; b1ef
ss5	jsr $ffcc		; clrch
	lda $b8		; file#
	jsr $ffc3		; close
	jmp endsmb

; ----------------------------------------- 	
; b1fa
distr	jmp ($00f8)

; ----------------------------------------- 
; b1fd
-	tax		; hibyte (to $b5)
	bit $fa
	bvs +   		; V-Flag = 1: Grafik
	bvc ++		;          0: Text

; ----------------------------------------- 
; b204
intro	jsr scrsvplus	; new: check for enhanced params
	jsr safeopen
	ldy #$00
	sty $fb		; fb/fc: 0400/c000/e000
	sty $fd 		; fd/fe: d800
	lda $b9		; sa: 2 (lores) or 3 or 5 (hires bitmap, hires+color):
	ror
	ror	
	ror	    	; Flags N/V: 10 (2) - 11 (3) - 01 (5)
	sta $fa 		; set saflag
; b217  			(change $b218 for 2K charsets, 2nd half)
cramhi	lda #$d8		; $d800
	sta $fe
; b21b 			(change $b21c to $cc for videoram after MEM!)	
vramhi	lda #$04		; videoram text merken für Startadresse
	bne - 		; uncond. branch
; b21f 			(change $b220 for any 8K RAM area)
bmphi
+	ldx #$e0		; default start address Hires
	lda #$c0		; Videoram bei Hires
++	sta $f7 		; merken
	lda #$ff		; Verteiler Hibyte
	sta $f9		; (an $00f8): $ff(cf/d2)
	rts

; ----------------------------------------- bef TEXT
; b22a			optimized
beftext	jsr getxy		; get X/Y
	jsr schkcom	; get string (address in $20/$21, len in $69 and helpvec)
	jsr sgetstr
	sta $21
	stx $20
	lda $69
	sta helpvec

	ldx #1		; preset to 1
	jsr getpktf	; anything following? yes, get MODE (0=clear, 1=set...) to $f7
	ldx #1		; preset to 1
	jsr gnextpar	; anything following? yes, get ZOOM factor (.x to $cb6e)
	txa
	bne +
	inx
+	stx chgr
	ldx #8		; preset to 8
	jsr gnextpar	; anything following? yes, get KERNING (.x to $c644)
	stx krx		; .y=0

	jsr textit	; write text

	jmp endsmb	; until finished

; ----------------------------------------- 

textit	ldy #0
	sty schrart	; preset textmode
	sty $6a		; init rvs flag
	beq txtst		; uncond. branch, any text ?

txloop	lda $a4		; (loop:) yes, save Y...
	pha
	lda $09		; X...
	pha
	lda $0a
	pha
	lda chgr		; and ZOOM
	pha

	lda ($20),y	; get char from string (.y: index into string)

	cmp #$12		; rvs on?
	beq +
	cmp #$92		; rvs off?
	bne ++
	lda #0		; no
	!by $2c
+	lda #$80		; yes
	sta $6a		; so set rvs flag

++	tax		; ctrl a (upper-graph)?
	dex
	beq +		; yes, flag is already set
	dex 		; ctrl b (upper-lower)?
	bne ++		; no, is any char
	ldx #1		; yes, set flag to 1
+	stx schrart
	jmp txcont1	; and get next char

++	cmp #$ff		; PI?
	bne +
	lda #$5e		; bcode for PI
	bne txl1

+	cmp #$60		; just a char: convert to bcode
	bcc +
	and #$7f
	ora #$40
	bne txl1
+	and #$3f

txl1	eor $6a		; care for RVS
	sta $ac
	lda schrart	; care for mode (1= 2nd part of charset)
	sta $ad
	tya		; save string index .y
	pha
	jsr charzei	; output char
	pla
	tay		; get .y back

txcont0	ldx #0		; flag for ctrl codes (0: inc X)
	!by $2c
txcont1	ldx #1		; (1: yes, ctrl code, don't inc)

	pla		; reget ZOOM...
	sta chgr
	pla		; X...
	sta $0a
	pla
	sta $09
	pla		; and Y
	sta $a4
	txa		; get ctrl code flag
	bne +

	clc		; set, so skip to next X position
	lda $09
	adc krx		; add kerning to X
	sta $09
	bcc +
	inc $0a

+	iny 		; proceed to next char in string
txtst	cpy helpvec
	bne txloop
	rts		; finished

; ----------------------------------------- 
; b2db
doelse	ldy #0
	ldx doindex	; is DO active?
	beq +		; no, skip ELSE, cont. prg
	lda justif	; from ELSE: is just an IF?
	bne +		; yes, get latest IF boole
	sty justif
	dex		; no, get DO-boole from stack
	lda dobooles,x	; (if DO is true: skip to DONE)
-	rts

+	pla		; if not: from most recent IF
	pla
	sty justif	; clear justif, cont. prg
	jsr fromelstk	; fetch boole, id IF true?
	bne +
	jmp execbef	; no, false
+	jmp befproc	; yes, true, skip rest of line

; ----------------------------------------- 
; b2ff
skipif	jsr schelse	; search for single ELSE:
	tax
	beq -		; found line end (yes, leave)
	iny
	lda (progz),y	; line end behind ELsE?
	bne  -		; no (so: single ELSE)
	pla
	pla
	jmp fndelse+4

; ----------------------------------------- 
; b30f
chkstate	jsr $ffcf
	ldx status
	beq nrm04
	pla
	pla
	jmp ss5

; ----------------------------------------- 
; b31b
jstifoff	sty justif
	jmp chrget

; ----------------------------------------- bef CSET
; b321			modified
befcset	jsr sgetbytn
	txa		; cset 0?
	beq +
	dex		; cset 1?
	bne ++

	lda #$17		; CSET 1: lower case chars
	!by $2c
+	lda #$15		; CSET 0: upper case chars
	jsr csetmem	; check for MEM, set charset
	bne +++		; uncond. branch

++	jsr nrm03		; cset 2
+++	jmp endsmb

nrm03	lda #$3b		; CSET 2: bits for hires mode
	sta $d011
	lda #$0b		; graphics at $e000, cols at $c000
	sta $d018
	lda #$94		; vic bank 3 ($c000)
	sta $dd00
	lda #10		; flag for MOB SET
	sta gmemflag
nrm04	rts

; ----------------------------------------- bef VOL
; b34f			enhanced
befvol	jsr chkonoff
	stx $d418
	jmp endsmb

; ----------------------------------------- bef DISK
; b358			recoded
befdisk	jsr chkdrv	; is drive there? 
	jsr sgetstrn	; yes, get string
	lda $69
	sta $b7
	stx $bb
	sty $bc
	lda $be		; set TSB drive
	sta $ba
	jsr $ffb1		; sys send listen
	lda #$6f
	sta $b9
	jsr $ff93		; sys send sa after listen
	ldy #0
-	lda ($bb),y	; send disk command
	jsr $ffa8		; sys cout
	iny
	cpy $b7
	bcc -
	jsr $ffae		; sys unlisten
	lda $9d		; direct mode?
	beq +
	jsr dec7a
	jmp flpmsg2	; bfc5 yes, to bef ERROR
+	jmp endsmb	; no, end command

; ----------------------------------------- main part of USE
; b390			recoded
use7	ldx #0		; clear current
	ldy $44
-	cpy $43		; compare to len
	bcs usel1
	lda ($49),y	; get char in ctrl string
	cmp #$2a		; "*" (leading char)?
	bne +
	sta $63
	beq ++
+	cmp #$23		; "#" (place holder)?
	bne usel1
++	inx		; yes, skip
	iny
	bne -

usel1	stx $66		; save index
	ldy #0
	ldx $61		; get index in out string
-	lda linenum+1,x	; get next char
	cmp #$2f		; digits and math signs?
	bcc +
	cmp #$45		; or "e"?
	beq usel2
	inx
	iny
	bne -
+	tya
	sta $65
	rts

; ----------------------------------------- 
; b3c3
usel2	pla
	pla
usel3	ldy $bd
--	cpy $43		; len of ctrl string
toul3	beq use5		; reached, cont in use3
	lda ($49),y	; get char from ctrl string
	ldx #6
-	cmp tsbpudef-1,x	; is a ctrl char?
	beq +		; yes, found
	dex
	bne -
	beq use5		; no

+	sta $02a7,y	; and store
	iny
	bne --

; ----------------------------------------- 
; b3df
err11	ldx #$0b		; error "syntax"
	jmp ($0300)

; ----------------------------------------- 
; b3e4
use3	lda $69		; len=0?
	beq err11		; yes, syntax error
	jsr use4		; set string parms (ac46)

; ----------------------------------------- 
; b3e8
	ldy #0
use5	sty $44		; current char in ctrl string
useloop	jsr mkoutstr
	jsr use7
	bne +

	ldx $66
	beq usel4
	jsr use8		; generate outstring (a5f9)
	jsr use9		; generated string to out (bfe9)
	ldy $44
	lda #$30		; "0"
	sta $02a6,y
	bne ++

+	lda $66
	sec
	sbc $65
	beq +
	bcc usel3
	jsr use8-1	; init outstring (a5f8)
+	jsr use9		; generate string
usel4	ldy $44

++	cpy $43
	beq useloop
	ldx $61
	lda linenum+1,x
	beq +
	inc $61
+	lda ($49),y
	cmp #$2c		; comma?
	beq +
	cmp #$2e		; period?
	bne usel5

+	jsr gno0
	jsr use7
	ldx $66
	beq usel5
	cmp $66
	bcs +
	jsr use9		; generate string
	lda $66
	sec
	sbc $65		; result: Anzahl Nachkommastellen
	jsr use9a
	beq usel5
+	txa
	jsr usel9
	cmp #$39		; "9"?
	beq usel5
	lda linenum+2,x
	cmp #$35		; "5"?
	bcc usel5
	ldx $44
	inc $02a6,x

usel5	jsr longround
	jmp useloop

; ----------------------------------------- 
; b467
userdy	jmp useready

; ----------------------------------------- 
; b46a
mkoutstr	ldy $44		; index to current char
	cpy $43		; = len?
	beq userdy	; yes, ready

	lda ($49),y	; compare to control chars
	ldx #6
-	cmp tsbpudef-1,x
	beq usea		; found!
	dex
	bne -
	sta $02a7,y	; store unchanged
	inc $44		; inc current
	bne mkoutstr	; and loop

usea	sty $bd		; store index of found
	lda #$2c		; running char: comma?
	jsr use6		; bad mode if not
	jsr sfrmnum	; get var
	jsr sfacasc	; convert to digits in linenum

	ldx #0		; index in linenum
	stx $61
	stx $63
usel6	ldx linenum	; get first char
	ldy $44		; get current position in ctrl string
	lda ($49),y	; get char 
	cmp #$2b		; "+"?
	bne +		; no
	cpx #32		; first char: 32 (=space)?
	bne usel0		; no, take this char
	ldx #$2b		; yes, set 43 (="+")
usel0	txa
	inc $44
	sta $02a7,y	; and store
	rts

+	cmp #$2d		; "-"?
	beq usel0		; yes, store
	rts		; no, leave it

; ----------------------------------------- Part of CENTRE
; b4b3
gcentpar	jsr frominsrt	; get string to be centered
	stx helpvec	; address
	sty helpvec+1
	lda $69		; len
	sta helpvec+2
	ldx #40		; 40: width of screen line
	jsr chrgot	; anything following?
	beq +
	jsr sgetbytc	; yes, get new width
+	txa
	cmp #40		; is (still) 40?
	bne +		; no, so use that width
	sbc $d3		; otherwise: subtract pos from 40
+	rts

; ----------------------------------------- bef HRDCPY
; b4d0			recoded
befhrdcpy	jsr open14	; open printer
	ldx $0288		; set video ram ($cc after MEM!)
	dex
	stx $21
	lda #$d8
	sta $20
	ldx #0		; .x counts rows
	lda #$91		; set upper case chars
	jsr bsout
	jsr lineout	; .y=0

hdy1	lda ($20),y	; read char from screen
	cmp #$ff		; is it PI?
	beq hdy3		; yes
	cmp #$80		; is it rvs?
	bcc hdy0
	eor #$80		; yes, so:
	pha
	lda #$12		; sys send rvs on
	jsr bsout
	pla
hdy0	cmp #$20		; char is upper case or no letter?
	bcs +
	ora #$40		; no, make it PETSCII lower
	bne hdy2

+	cmp #$40		; no, is a upper case or a graphic char?
	bcc hdy2		; no
	ora #$80		; yes, prepare to print it
	!by $2c
hdy3	lda #$de		; this is PI!
hdy2	cmp #$22		; char is a quote?
	bne +		; no, print
	txa		; yes, save .x
	pha
	ldx #6
-	lda quotetab,x	; send graphical bit sequence for quote char
	jsr bsout
	dex
	bpl -
	pla		; restore .x
	tax
	lda #$0f		; send ctrl o (graphics off)
+	jsr bsout
	lda #$92		; send rvs off
	jsr bsout

	iny		; then proceed:
	cpy #40		; one screen row?
--	bne hdy1

	jsr lineout
	inx
	cpx #25		; all rows?
	bne --		; no, loop

	jsr sclose	; close printer
	jmp bef0		; finished

; ----------------------------------------- 
; b53b
charmode	pha
	lda $d018		; lower case?
	cmp #$17
	bne +
	lda #$11		; yes, send crsr dwn 
	jsr bsout
+	pla
	rts

; ----------------------------------------- 
; b54a
lineout	ldy #5		; patch to #6 (no line gaps anymore)
-	lda nline-1,y
	jsr bsout
	dey
	bne -
	jsr charmode	; change case if lower case is set
	jmp l20pl40

; ----------------------------------------- 
; b55b
quotetab	!by $80,$80,$83,$80,$83,$80,$08
nline	!by $31,$32,$10,$0f,$0d,$08

; ----------------------------------------- 
; b568
	!by 0,0,0

; ----------------------------------------- part of DO
; b56b
donecont	dec doindex	; from DONE: dec stack pointer
docont	jsr chkdost	; from DO:
	jmp stofdo2	; set DO (in DODO) active (if indoloop=1)

; ----------------------------------------- bef DO
; b573			new
befdo	ldy #0		; check for "NULL" if not end of line
	jsr jstifoff	; clear justif/end of line?
	beq docont	; yes
	jmp isnull	; no

; ----------------------------------------- part of TRACE
; b57e
trc1	sta $22
	stx $23
	sty $69
	jmp rtc1

; ----------------------------------------- bef KEY
; b587			enhanced
befkey	jsr keyget	; check for KEYGET (doesn't return if successful)
	bne +		; last byte is delimiter?
	txa		; yes, set flag
	eor #10
	sta keyflag	; (10: tap f-key is off)
--	jmp endsmb	; (21)
; b595
+	cpx #$11		; more than 16?
	bcc +
-	jmp swarm		; yes, bad mode error
; b59c
+	dex		; parameter after KEY 0?
	bmi -		; yes, bad mode (### v2.20830)
	stx $aa		; times 16
	lda #0
	asl $aa
	rol
	asl $aa
	rol
	asl $aa
	rol
	asl $aa
	rol
	sta $ab

	clc		; plus $c64d (keytab)
	lda $aa
	adc #<keytab
	sta $aa
	lda $ab
	adc #>keytab
	sta $ab
	jsr schkcom
	jsr sgetstr	; get string
	sta $a9
	stx $a8
	ldy #0
-	lda ($a8),y	; and store
	cmp #$5f		; left arrow?
	bne +
	lda #$0d		; yes, replace by cr
+	sta ($aa),y
	cpy $69
	beq +
	iny
	cpy #16		; max 16 chars
	bcc -
	beq --
+	lda #0
	sta ($aa),y
	beq --

; ----------------------------------------- Part of FLASH
; b5e5
flashinit	lda #0
	sta flashzae
	jmp chkonoff	; Parameter holen

; ----------------------------------------- Part of MEMLOAD/SAVE
; b5ed 			by P. Hagemann
execdma	bmi +		; carries FF00 flag
	lda #$30		; no color ram access wanted
	bne ++
+	lda #$35		; color ram access wanted
++	sei
	sta pport
	lda $ff00		; save byte
	sta $ff00		; activate DMA
	pla		; restore IO config
	sta pport
	cli
	jmp bef0		; finished

; ----------------------------------------- bef PAINT
; b605			recoded
befpaint	jsr getxy
	jsr getpktf
	stx $66

	lda #11		; test
	sta $f7
	jsr punkt
	lda status	; result (1=set,0=unset)
	sta $6a
	cmp $66
	beq +
	jsr pnt0
+	jmp endsmb

; b622
pnt0	lda ax
	sta $5c
	lda ax+1
	sta $5d
	lda ay
	sta $5b
	lda #1
	jsr pnt1
	lda #0
	sta $f9
	lda $5b
	sta pdot
	tsx
	stx pstack
; b643
recurs	tsx		; now go!
	cpx #20		; stack runs low?
	bcc pntend	; yes, end
	lda $fd
	pha
	lda $fe
	pha
	lda $5c
	pha
	lda $5d
	pha
	lda $5b
	sta $fd
	lda pdot
	sta $fe

--	jsr pnt2
	bcc pnext
	jsr pset
	bcc pnext
	lda $5b
	pha
	lda pdot
	pha
-	jsr pset
	bcc +
	jsr recurs
	bcc -
+	pla
	sta $fe
	pla
	sta $fd
	lda $f9
	eor #1
	sta $f9
	jsr pnt2
-	jsr pset
	bcc +
	jsr recurs
	bcc -
+	lda $f9
	eor #1
	sta $f9
	jsr pnt2
	lda $028d		; shift/ctrl/c=?
	beq --

	ldx pstack	; yes, break
	txs
	rts

; ----------------------------------------- 
; b6a4
pnext	pla
	sta $5d
	pla
	sta $5c
	pla
	sta $fe
	pla
	sta $fd
pntend	rts

; ----------------------------------------- 
; b6b1
pset	lda $fd
	sta $5b
	lda $fe
	sta pdot
--	jsr testfrei
	bne pnt3
-	lda $5b
	beq +
	dec $5b
	jsr testfrei
	beq -
	inc $5b
+	lda $5b
	pha
-	jsr testfrei
	bne +
	jsr pset1
	inc $5b
	lda $5b
	cmp #$c8		; 200? (bottom)
	bne -
+	dec $5b
	lda $5b
	sta pdot
	pla
	sta $5b
	cli
	sec
	rts

; ----------------------------------------- 
; b6ec
pnt3	inc $5b
	lda pdot
	cmp $5b
	bcs --
	rts

; ----------------------------------------- 
; b6f6
pnt2	lda $f9
pnt1	and #1
	beq pnt4
	lda $5c
	ora $5d
	beq pnt7
	lda $5c
	bne +
	dec $5d
+	dec $5c
	sec
	rts

; ----------------------------------------- 
; b70c
pnt4	lda multijn
	bne pnt5
	lda $5d
	beq pnt6
	lda $5c
	cmp #$3f
	beq pnt7

pnt6	inc $5c
	bne +
	inc $5d
+	sec
	rts

pnt5	lda $5c
	cmp #$9f
	bne pnt6
pnt7	clc
	rts

; ----------------------------------------- 
; b72b
testfrei	lda $5c
	ldx $5d
	ldy $5b
	sta $09
	stx $0a
	sty $a4
	lda #11		; test
	sta $f7
	jsr punkt
	jsr kerromein
	lda $66
	sta $f7
	lda status
	cmp $6a
	rts

; ----------------------------------------- 
; b74a
pset1	lda $5c
	ldx $5d
	ldy $5b
	sta $09
	stx $0a
	sty $a4
	jsr punkt
	jmp kerromein

; ----------------------------------------- 
; b75c
pdot	!by $36
pstack	!by $e6

; ----------------------------------------- 
; b75e
;
; End of part 5
;



