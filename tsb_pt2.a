; ----------------------------------------- 
;
; Tuned Simons' Basic
;
; by Arndt Dettke, 1986
;
; part 2
; patched on 11.01.2020
; patched on 11.02.2020 (func INSERT)
; patched on 24.03.2021 (added CHKDRV)
; patched on 04.04.2021 (command COLD)
; patched on 20.04.2021 (bugfix in func $)
; patched on 04.05.2021 (bugfix in func CHECK)
; patched on 07.12.2021 (optimized)
; patched on 13.03.2022 (bugfix in warmstart caused by ON ERROR/END)
; patched on 06.06.2022 (optimized, added func D!PEEK)
; patched on 27.06.2022 (bugfix in VERIFY)
; patched on 14.07.2022 (bugfix in INST, optimized code)
; patched on 03.08.2022 (added illegal direct to INST, INSERT & PLACE)
; patched on 01.10.2022 (added error message to command DO..DONE)
;
; ----------------------------------------- 

	!ct pet

; ----------------------------------------- TSB Functions
; 88bc			recoded
fnjoy	ldy #$00		; set $dc00
	sty helpvec
	lda #$dc
	sta helpvec+1
	jsr chrgot	; anything more?
	beq +		; .y=0

	jsr $aefa		; yes, "("?
	jsr sgetbyt1	; value to .x
	jsr schkklz	; ")"?
	txa
	and #1		; delimit to 0/1
	tay

+	lda (helpvec),y	; get joy
	sta $20
	and #$6f		; skip fire
	sta joywert

	ldx #0
-	lda tabjoy,x	; convert to joy value
	inx
	cmp joywert
	beq +
	cpx #8
	bne -

	lda #0		; not in tab, so $00
	sta joywert
	beq ++

+	stx joywert	; we have a joy value

++	lda $20		; now isolate fire
	and #$10
	bne +
	lda joywert	; and indicate in value
	ora #$80
	sta joywert

+	ldy joywert	; convert to float
	jmp sxyflp3	; $8CCB

; ----------------------------------------- 
;
tabjoy	!by $6e,$66,$67,$65,$6d,$69,$6b,$6a

; ----------------------------------------- func POT
; 8914			modified
fnpot	jsr $aefa		; sys "("?
	jsr sgetadr2	; (- jsr klgetadr!)
	jsr $aef7		; sys ")"?
	lda $65
	bne +
	ldy $d419		; get paddle 0 value
	jmp ++
+	ldy $d41a		; get paddle 1 value
++	sty $63
	jmp sxyflp3	; convert to float

; ----------------------------------------- func PENX
; 892f			original
fnpenx	jsr penabfr
	ldy lpx		; lightpen x
	jmp sxyflp3

; ----------------------------------------- func PENY
; 8938			recoded
fnpeny	ldy lpy		; lightpen y (do first: PENX!)
	jmp sxyflp3
; 893e
penabfr	lda $d019
	and #8		; wait for strobe
	beq penabfr
	lda $d013		; get lp x
	sta lpx
	lda $d014		; get lp y
	sta lpy
	lda $d019		; clear strobe
	and #$f7
	sta $d019
	rts

; ----------------------------------------- 
; 895a
trace1	lda $d3		; save cursor
	pha
	lda $d5
	pha
	lda $d6
	pha
	lda $d4
	pha

	lda $028d		; C= key?
	and #$02
	beq +

---	ldx #8		; yes, slowdown
	stx helpvec
--	ldx #$2d
	ldy #0
-	dey
	bne -
	dex
	bne -
	dec helpvec
	bne --

+	lda $028d		; Shift?
	and #$01
	bne ---		; yes, slowdown

	jsr $e566		; sys home
	jsr trace2
	jsr $aad7		; sys out cr/lf
	ldx helpvec
	jsr $e88c		; sys set cursor
	ldy helpvec+1
	sty $d3
	lda ($d1),y	; revert char
	eor #$80
	sta ($d1),y

	pla		; restore cursor
	sta $d4
	pla
	tax
	jsr $e88c		; sys set cursor
	pla		; restore cursor
	sta $d5
	pla
	sta $d3
-	rts

; ----------------------------------------- 
; 89ad
trace3	lda #$20
	ldy $d3
--	sta ($d1),y
	cpy $d5
	bcs -
	iny
	bne --
; 89ba
trace2	jsr $bdc9		; sys out: line # (.x/.a)
	lda $39
	ldx $3a
	sta int
	stx int+1
	jsr $a613		; sys compute start of line
	lda $d6
	ldx $d3
	sta helpvec
	stx helpvec+1
	ldy #3
	sty $0b
	sty $49
	lda #$20

tr2	ldy $49
	and #$7f
; 89dc
tr1	jsr $ab47		; sys out char in .a
tr21	cmp #$22		; string?
	bne tr0
	lda $0b		; contains: 3
	eor #$ff
	sta $0b
tr0	iny
	ldx $60
	tya
	clc
	adc $5f
	bcc +
	inx
+	cmp progz
	bne +
	cpx progz+1
	bne +
	lda $d6
	ldx $d3
	sta helpvec
	stx helpvec+1
+	lda ($5f),y
	beq trace3
	bmi ++
	cmp #$64		; sb token?
	bne +
	jsr tr3		; yes
	ldy $49
	bne tr0

+	jmp tr1
++	cmp #$ff
	beq tr1
	bit $0b
	bmi tr1
	jsr trace4
	bmi tr20
; 8a24
trace4	ldx #$a0		; sys: address of basic commands
	stx $21
	ldx #$9e
	stx $20
; 8a2c
trace7	sty $49		; points via $5f to token number
	tax		; .a: token number as count down
	ldy #0
	asl		; times 2
	beq ++		; was 0? (HIRES)
--	dex		; no, count finished?
	bpl +
-	jsr inc20		; yes, not found: skip keyword
	lda ($20),y
	bmi --		; char shifted? (delimiter): count
	bne -		; no, proceed skipping
-
+	iny		; found: out keyword
++	lda ($20),y
	php
	and #$7f
	jsr $ab47		; no, sys: out char in  .a
	plp
	bmi +
	bne -		; uncond. branch

tr3	iny
	jmp trace5

; ----------------------------------------- 
; 8a52
tr20	ldy $49
	bne tr21

; ----------------------------------------- Part of PLAY
; 8a56
sirqplay	lda musicz4	; from sirq5
	beq sip0
	dec musicz2
	lda musicz2
	beq sip1
+	rts

sip1	dec musicz4
	lda musicz4
	beq sip0
	lda musicz3
	sta musicz2
	rts

sip0	lda musicadr	; get adr of music string
	sta $fc
	lda musicadr+1
	sta $fd
	ldy musicz1	; get pointer into string
	cpy musiclen	; at end of string?
	bcs sip2		; yes, end PLAY

	lda ($fc),y
	cmp #$8d		; beyond f8? (code for longest note)
	bcs sip9		; yes
	cmp #$85		; below f1? (code for shortest note)
	bcc sip9		; yes
	sbc #$84		; code valid, set individ. note duration
	sta musicz2
	sta musicz3
	lda musicdau
	sta musicz4
	bne sip4		; uncond. branch
; 8a9f
sip2	sty musicz5	; store pointer into string
	jsr notesuch	; search note, result in .a and .x
	ldy #1
	sta ($fe),y
	txa
	dey
	sta ($fe),y
	jsr stimmein	; play on
	ldy musicz5	; re-get pointer

sip4	cpy musiclen	; at end of music string?
	bcs playend	; yes
	iny		; no, advance
	sty musicz1
	rts
; 8abd
playend	lda #0		; clear play flag
	sta musicz1
	sta playflag
	rts

sip9	cmp #$93		; clr (code for select voice)?
	bne sip2		; no, note?
	iny		; yes, get voice number
	lda ($fc),y
	ldx #$00
	stx musicz4
	cmp #$33		; beyond voice 3?
	bcs sip5
	cmp #$31		; below voice 1?
	bcc sip5
	sbc #$30		; make value from voice #
	tax
	dex
	lda #$d4		; set $d4xx (from voice)
	sta mregadr+1	; 
	lda mregs,x	; pointer to voice 1..3
	sta mregadr
	stx stimmenr
-	jmp sip4		; advance in music string
; 8aef
sip5	cmp #$47		; code "g": voice off?
	bne sip3
	tya
	pha
	jsr svmregadr
	ldy stimmenr
	lda wavetab,y
	and #$fe
	ldy #4		; same code as below!
sip11	sta ($fe),y
	pla
	tay
	bne -		; advance
; 8b08
sip3	cmp #$54		; code "t": sync bit?
	bne sip6
	tya
	pha
	jsr svmregadr
	ldy stimmenr
	lda wavetab,y
	eor #$02
--	ldy #4
	bne sip11		; advance
; 8b1d
sip6	cmp #$43		; code "c": clear waveform
	bne sip7
	tya
	pha
	jsr svmregadr
	lda #0
	beq --

sip7	cmp #$52		; code "r": repeat music
	bne -
	ldy #0
	sty musicz1
	sty musicz4
	rts

; ----------------------------------------- 
; 8b37
notesuch	ldy #$13		; search in 20 notes
-	cmp nottab,y
	beq sip8
	dey
	bpl -
	lda #0		; not found
	tax
	beq svmregadr
; 8b46
sip8	lda frqtabl,y	; found, set frequency
	sta mfreq
	tya		; hi byte:
	ldy #0
	cmp #12		; 12=0
	beq +++
	cmp #19		; 19=2
	beq +
	cmp #11		; 11=2
	bne ++
+	iny
++	iny		; all others=1
+++	sty mfreq+1
	inc musicz5
	ldy musicz5	; set octave
	lda ($fc),y
	and #15		; (0..max 9)
	tax
	beq sip10
-	asl mfreq
	rol mfreq+1
	dex
	bne -
; 8b76
sip10	lda mfreq+1
	ldx mfreq
svmregadr	ldy mregadr
	sty $fe
	ldy mregadr+1
	sty $ff
	rts

; ----------------------------------------- 
; 8b87
mregs	!by $00,$07,$0e
; 8b8a
nottab	!by $43,$c3,$44,$c4,$45,$46,$c6,$47
	!by $c7,$41,$c1,$42,$5a,$ac,$b1,$bb
	!by $a5,$b0,$bf,$bc
; 8b9e
frqtabl	!by $12,$23,$34,$46,$5a,$6e,$84,$9b
	!by $b3,$cd,$e9,$06,$00,$23,$46,$5a
	!by $84,$b3,$e9,$06

; ----------------------------------------- 
; 8bb2
stimmein	ldy #6		; index to SID register 6
	ldx stimmenr
	lda srtab,x	; get sustain/release values from buffer
	sta ($fe),y	; write to register
	dey		; register 5
	lda adtab,x	; get attack/decay values from buffer
	sta ($fe),y
	dey		; register 4
	lda wavetab,x	; set waveform data
	and #$fe		; clear key bit
	sta ($fe),y
	ora #1		; and set it (activate voice)
	sta ($fe),y
	rts

; ----------------------------------------- bef GRAPHICS
; 8bcf			new
befgraphics
	ldx #<namehsg	; load high speed graphics
	ldy #>namehsg
	jsr loadext
	bcs +
	jsr basromei
	jsr $7000		; init HSG
	jsr $a660		; sys CLR
	jmp bef0

+	jmp iffail1

; ----------------------------------------- New LOAD/SAVE
; 8be7			new
sysload	ldy drive
	sty $ba
	jsr chrgot	; more?
	beq +
	jsr bloadadr	; yes, get bload adr
+	lda $0a		; ### 2.20627 (VERIFY-Patch)
	jmp $f4a5		; sys LOAD
; 8bf8
syssave	ldy drive
	sty $ba
	jmp $f5ed		; sys SAVE

; ----------------------------------------- 
; 8bff
loadsave	!wo sysload,syssave

; ----------------------------------------- 5
; 8c03
cold2	ldy #3
-	lda loadsave,y
	sta $0330,y	; sys set vectors
	dey
	bpl -
	rts

; ----------------------------------------- Check Drive Presence
; 8c0f			(P. Hagemann)
chkdrv	lda #0
	sta status
	lda drive
	jsr $ffb1		; sys: LISTEN
	jsr $ffae		; sys: UNLISTEN
	lda status	; what happened?
	bmi err05
-	rts

; ----------------------------------------- 
; 8c21
gpf2	ldx mobsize	; get current mobsize
	jsr gnextpar	; get new one to .x (or keep .x and skip)
gpf3	stx mobsize	; set new mobsize
	rts

; ----------------------------------------- 
; 8c2a
safeopen	jsr sopen
	bcc -

err05	ldx #5		; drive not ready
	jmp ($0300)

; ----------------------------------------- 
; 8c34
mobexs	lda $d01d
	ora mbittabs,x
	bne +

mobeys	lda $d017
	ora mbittabs,x
	bne ++

mobexl	lda $d01d
	and mbittabl,x
+	sta $d01d
	rts

mobeyl	lda $d017
	and mbittabl,x
++	sta $d017
	rts

; ----------------------------------------- 
; 8c58
chkdost	lda doindex	; stack pointer DO
	cmp #2
	bcs faildo
	jmp dec7a

faildo	jmp err2a		; "stack too large"

; ----------------------------------------- 
; 8c65
sclose	jsr $ffcc		; sys clrch
	lda #1
	jmp $ffc3		; sys close 1

; ----------------------------------------- 
; 8c6d
sgetstr1	lda portspei
	pha
	jsr sgetstr
	sta zwspei1
	pla
	sta portspei
	lda zwspei1
	rts

; ----------------------------------------- Arithmetical Expression
; 8c7f			enhanced
sgetari	lda #0
	sta $0d
	jsr chrget
	bcs +		; 8c94
	jmp sascflp

+	cmp #$24		; "$"?
	beq gethex
	cmp #$25		; "%"?
	beq getbin
	cmp #$64		; sb token?
	beq +
	jmp sgetari1	; no : sys
+	jmp csgetari1	; yes: tsb

; ----------------------------------------- 
; 8c9d
chhexchr	jsr chrget	; get next char, is digit?
	bcc ++		; yes, ok
; 8ca2
ishex	cmp #"g"		; no, beyond "f"?
	bcs ++		; yes, not hex
	cmp #"a"		; below "a"?
	bcs +		; no, ok (is a-f)

	sec		; fail (is no hexdigit)
	rts
+	clc		; ok (is hexdigit)
++	rts
; 8cae
err25	pla
	pla
	ldx #$25		; error "not hex char"
	jmp ($0300)

; ----------------------------------------- func %
; 8cb5
getbin	jsr getdouble
	jsr bincon
	ldy $a8

; ----------------------------------------- to Float
; 8cbd
sxyflp3	ldx #0
sxyflp	sty $63		; lo
	stx $62		; hi
sxyflp2	ldx #$90
	sec
	jmp $bc49		; sys convert to float

; ----------------------------------------- func $
; 8cc9
gethex	ldx #0
	stx $a5
	jsr getdouble	; check wether $$, returns .y=1
; 8cd0
hcon0	jsr dec7a

hexcon	jsr onebyte	; first two digits of hex value
	tay		; in .y (.x=0)
	lda $a5		; max position?
	jsr byte2		; has two digits?
	bcs hcon1		; yes, out
	tya		; has 4 digits, so first two are hibyte
	tax		; 1st is now hibyte
	jmp hcon0		; loop for 2nd two digits

hcon1	jmp sxyflp

; ----------------------------------------- 
; 8ce6
onebyte	jsr chhexchr	; hi nibble digit valid?
	bcs err25		; no, error
	tay
	jsr chhexchr	; lo nibble digit valid?
	bcs err25
	jmp hex2w		; yes, convert to byte

; ----------------------------------------- 
; 8cf4
nxline	jsr sbzply
	ldy #2		; found prog end?
	lda (progz),y
	beq +		; yes, to RTS
	ldy #5
--	jsr sbzply
; 8d02
sfordone	jsr schdo		; search for DO
	cmp #0		; found line end?
	beq nxline	; continue search
	ldx #$ff
-	inx
	iny
	lda (progz),y
	cmp tuned+5,x	; compare against "ne" (is "done"?)
	beq -
	cpx #2
	bcc --
	jmp spz2done	; set PZ to 1 before token of DONE

; ----------------------------------------- 
; 8d1b
hex2w	inc $a5		; inc byte counter
	jsr hexw		; convert .a und .y to byte...
	sta zwisp2
	tya
	jsr hexw
	asl
	asl
	asl
	asl
	ora zwisp2	; ...return in .a
+	rts

hexw	sec		; make value from digit
	sbc #$30
	cmp #10
	bcc +
	sbc #7
+	rts

; ----------------------------------------- func EXOR
; 8d39			modified
fnexor	jsr getexor	; check "(", get first value
	sty $aa
	sta $ab
	jsr $aefd		; sys chkcom
	jsr sgetadr2
	jsr intback	; restore INT value, sys ")"
	lda $65
	eor $aa
	tay
	lda $64
	eor $ab
	tax
	jmp sxyflp

; ----------------------------------------- func DUP
; 8d56			enhanced
fndup	jsr $aefa		; sys "("?
	jsr sgetstr1	; get string to copy
	ldy $69		; len
	sty pos
	sta str1+1
	stx str1

	jsr sgetbytc	; get copy factor
	jsr $aef7		; sys ")"?
	ldx $65		; factor
	beq err14
	stx dwert

	ldy #0
	sty pos1
--	ldy #0		; multiply len
-	inc pos1
	beq jerr17	; if more than 255: error
	iny
	cpy pos
	bne -
	dex
	bne --

	lda pos1		; get new len
	jsr neustr	; create string (adr in $62)
	lda str1+1	; get address of string to copy
	sta $21
	lda str1
	sta $20

	ldx dwert		; get factor
	ldy #0
	sty helpvec	; count full len
-	lda ($20),y	; copy from original...
	sty helpvec+1
	ldy helpvec
	sta ($62),y	; ...to copy
	iny
	sty helpvec
	ldy helpvec+1	; count separate len
	iny
	cpy pos		; is reached?
	bne -
	ldy #0		; so back to 0
	dex		; count # of copies
	bne -

	lda #$ff		; publish new string
	sta $0d
	jmp pushstr

; ----------------------------------------- 
; 8dbf
err14	ldx #$0e		; error "illegal quantity"
	jmp ($0300)	; (affects "tsb.mem"!)
jerr17	jmp err17		; error "string too long"

; ----------------------------------------- 
; 8dc7
csgetari1	jsr chrget
	tay
	jsr chrget

	cpy #$55		; DUP?
	bne +
	jmp fndup

; ----------------------------------------- 

+	cpy #$5a		; EXOR?
	bne +
	jmp fnexor

; ----------------------------------------- 

+	cpy #$51		; JOY?
	bne +
	jmp fnjoy

; ----------------------------------------- 

+	cpy #$0e		; FRAC?
	bne csgetari2

; ----------------------------------------- func FRAC
; 8de7			enhanced
fnfrac	ldy #0
	jsr sxyflp3
	jsr $bc0c		; sys fac->arg
	jsr $aefa		; sys "("?
	jsr $ad9e		; sys frmevl
	lda $66		; save sign
	sta $aa
	bpl +
	jsr $bfb4		; sys toggle sign
+	jsr $aef7		; sys ")"?
	jsr $bc1b		; sys runden fac
	jsr $bc0c		; sys fac->arg
	jsr $bccc		; sys INT()
	jsr $b853		; sys arg-fac
	lda #0
	sta $0d
	lda $aa
	bpl +
	jsr $bfb4		; sys toggle sign
+	jmp $bc1b		; sys runden fac

; ----------------------------------------- 
; 8e1b
getdouble	ldy #1
	cmp (progz),y
	beq +
	jmp chrget

+	pha		; save current char
	jsr chrget
	jsr chrget
	jmp enhexbin	; func %%/$$

; ----------------------------------------- 
; 8e2e
getstrgs	jsr $aefa		; sys "("?
	jmp get2strs

; ----------------------------------------- 
; 8e34
csgetari2	cpy #$10		; PLACE?
	beq fnplace
	jmp csgetari3

; ----------------------------------------- func PLACE
; 8e3b			modified
fnplace	jsr getstrgs	; check "(", get 2 strings
	jsr $aef7		; sys ")"?

	lda $9d		; direct mode?
	bne err15		; yes, error

	tay		; search str1 in str2
	sty plpos		; position to 0
-	lda ($a7),y	; found 1st char?
	cmp ($a9),y
	bne +
	iny		; yes, check next
	cpy strlen1	; as long as str1?
	bne -
	inc plpos		; yes, position found, create value
	lda plpos
--	tay
	lda #0
	sta $0d
	jmp sxyflp3

+	ldy #0		; char not found yet, advance in oldstr
	inc $a9
	bne +
	inc $aa
+	inc plpos		; advance position likewise
	lda plpos
	cmp strlen2	; oldstr checked?
	bne -
	lda #0		; yes, result: 0
	beq --

; ----------------------------------------- 
; 8e7a
csgetari3	cpy #$5b		; INSERT?
	beq fninsert
	jmp csgetari4

; ----------------------------------------- func INSERT
; 8e81			recoded
fninsert	jsr getstrgs	; check "(", get 2 strings
	jsr getbytc	; get position ($c51f)
	stx pos
	jsr $aef7		; sys ")"?

	lda $9d		; direct mode?
	bne err15		; yes, error

	clc
	lda strlen2	; len of result string > 255?
	adc strlen1
	bcc +
; 8e9a
err22	ldx #$22		; yes, error "insert too large"
	!by $2c
err15	ldx #$15		; error "illegal direct"
	!by $2c
err17	ldx #$17		; error "string too long"
	jmp ($0300)

+	jsr neustr	; neuen String einrichten (62/3, len: 61)
			; hier: 

	ldy #0		; Zähler auf Null
	sty $bf		; auch für den zweiten Durchgang
	beq +
-	cpy pos		; Position erreicht?
	bcs ++
	lda ($a9),y	; nein, übertragen
	sta ($62),y
	iny
+	cpy strlen2	; Länge erreicht?
	bne -		; nein, loop (ja: weiter)

++	sty helpvec	; Position merken
	sty dwert		; auch für dritten Durchgang
-	ldy $bf 		; startet mit Null
	cpy strlen1	; Einfügelänge erreicht?
	beq +
	lda ($a7),y	; nein, holen
	inc $bf
	ldy helpvec	; einfügen
	sta ($62),y
	inc helpvec
	bne -		; loop

+	ldy dwert		; kein Rest?
	bne +		; nein, neuen String erzeugen

-	lda ($a9),y	; sonst: Rest übertragen
	inc dwert
	ldy helpvec
	sta ($62),y
	inc helpvec
	ldy dwert
+	cpy strlen2
	bcc -

jpushstr	lda #$ff		; create result string
	sta $0d
	jmp pushstr

; ----------------------------------------- 
; 8ef4
colsout	jsr $ff5b
	jmp tsbcols

; ----------------------------------------- 
; 8efa
frominsrt	jsr sgetstrn
	jmp basromau
	
; ----------------------------------------- Part of Warmstart
; 8f00
endcheck	jsr basromei
	txa		; any error?
	bpl +
eck0	lda #0		; no, clear on error flag
	sta onerrorflag
+	rts

; ----------------------------------------- RESTORE for SCRLD/SCRSV
; 8f0c by P. Hagemann
scrsvrst	ldx #4
	stx vramhi+1	; vram back to $0400
	stx nvrbl+1	; 4 pages
	ldx #$e0		; bitmap start at $e000
	stx bmphi+1
	ldx #$20		; 32 pages
	stx nbmpbl+1
	stx skipcr	; $20 = JSR
	stx skipvr
	stx skipslo
	stx skipshi
	jmp leave1	; finish

; ----------------------------------------- 
; 8f2d
idiv	lda #0
	sta idivrest
	sta idivrest+1
	lda idivsor
	ora idivsor+1
	bne +
	pla
	pla
	ldx #$14		; error "division by zero"
	jmp ($0300)

+	ldx #$10
-	rol idivdend
	rol idivdend+1
	rol idivrest
	rol idivrest+1
	sec
	lda idivrest
	sbc idivsor
	tay
	lda idivrest+1
	sbc idivsor+1
	bcc +
	sty idivrest
	sta idivrest+1
+	dex
	bne -
	rol idivdend
	rol idivdend+1
	rts

; ----------------------------------------- 
; 8f72
csgetari4	cpy #$53		; DIV?
	bne csgetari5

; ----------------------------------------- func DIV
; 8f76			original
fndiv	jsr divinit
	ldy idivdend
	ldx idivdend+1
	jmp sxyflp

; ----------------------------------------- 
; 8f82
divinit	jsr getint	; sys "("?
	jsr sgetadr2
	sta idivdend+1
	sty idivdend
	jsr $aefd		; sys chkcom
	jsr sgetadr2
	sta idivsor+1
	sty idivsor
	jsr intback	; ")"
	jmp idiv

; ----------------------------------------- 
; 8fa0
csgetari5	cpy #$52		; MOD?
	bne csgetari6

; ----------------------------------------- func MOD
; 8fa4			original
fnmod	jsr divinit
	ldy idivrest
	ldx idivrest+1
	jmp sxyflp

; ----------------------------------------- 
; 8fb0
csgetari6	cpy #$59		; LIN?
	bne csgetari7

; ----------------------------------------- func LIN
; 8fb4			modified
fnlin	sec
	jsr $fff0		; sys plot
	txa
	tay
	jmp sxyflp3

; ----------------------------------------- 
; 8fbd
csgetari7	cpy #$58		; TEST?
	bne csgetari8

; ----------------------------------------- func TEST
; 8fc1			modified
fntest	jsr klgetadr	; check "(", get adress value
	jsr getxy2	; get X/Y
	jsr $aef7		; sys ")"?
	lda #$0b
	sta $f7
	jsr basromau
	jsr punkt
	ldy status
	jsr basromei
	jsr kerromein
	cli
	jmp sxyflp3

; ----------------------------------------- Part of EXOR
; 8fe0
getexor	jsr getint	; check "(", get adress value
	jmp sgetadr2

; ----------------------------------------- Part of DO..DONE
; 8fe6			
doiftrue	lda #0		; doflag off
	sta doflag
	iny
	jmp chrgot	; get latest byte from prg

; ----------------------------------------- 
; 8fef 
mkwahr	lda $61		; DO found: store boole value on DO stack
	ldx doindex
	sta dobooles,x
	inc doindex	; inc stackpointer
	sty indoloop	; set flag for active DO
+	rts

; ----------------------------------------- 
; 8ffe 
execbef	jsr basromei
	jmp ($0308)	; sys get bef address

; ----------------------------------------- Part of DO
; 9004 
chkelse	jsr schelse	; search for ELSE in same line
	sta justif	; set flag for "ELSE in IF-line"
	rts		; (then: .a>0, if no ELSE: .a=0)

; ----------------------------------------- 
; 900b
restint	ldx helpvc2	; restore INT
	lda helpvc2+1
	stx int
	sta int+1
	rts

; ----------------------------------------- 
; 9014
csgetari8	cpy #$57		; INST?
	beq fninst
	jmp csgetari9

; ----------------------------------------- func INST
; 901b			enhanced
fninst	jsr getstrgs	; check "(", get 2 strings
	jsr getbytc	; get position ($c51f)
	stx pos
	jsr $aef7		; sys ")"?

	lda $9d		; direct mode?
	beq +
	jmp err15		; yes, error

+	clc
	lda strlen1	; newlen + pos > oldlen?
	beq +
	adc pos
	cmp strlen2
	bcc +
	beq ++
	jmp err17		; yes, "string too long"

+	lda strlen2	; otherwise: build newstring (62/63)
++	jsr neustr	; sys: set stringdescriptor

; 9047
	ldy #0
	sty $bf
	sty helpvec

-	lda ($a9),y	; get char from oldstring
	cpy pos		; at inst position?
	bcc ++		; no

	ldy $bf		; yes, inststring finished?
	cpy strlen1
	bcs +
	inc $bf		; no,
	lda ($a7),y	; get char from inststring
+	ldy helpvec

++	sta ($62),y	; write to newstring
	inc helpvec
	ldy helpvec	; all chars?
	cpy strlen2
	bcc -

	jmp jpushstr	; yes, create new string on stringheapn (40)

; ----------------------------------------- 
; 906f
get2strs	jsr sgetstr1	; get string
	sta $a8		; adr: a7/a8
	stx $a7
	lda $69		; len: strlen1 ($cbb7)
	sta strlen1
	jsr $aefd		; sys chkcom
get1str	jsr sgetstr1	; get oldstring
	sta $aa		; adr: a9/aa
	stx $a9
	lda $69
	sta strlen2
	rts

; ----------------------------------------- 
; 908b			enhanced
csgetari9	cpy #$56		; INKEY?
	bne csgetaria

; ----------------------------------------- func INKEY
; 908f			modified
	ldy inkey
	jmp sxyflp3

; ----------------------------------------- 
; 9095
csgetaria	cpy #$5c		; POT?
	bne csgetarib
	jmp fnpot

; ----------------------------------------- 
; 909c
csgetarib	cpy #$5d		; PENX?
	bne csgetaric
	jmp fnpenx

; ----------------------------------------- 
; 90a3
csgetaric	cpy #$5f		; PENY?
	bne csgetarid
	jmp fnpeny

; ----------------------------------------- 
; 90aa
csgetarid	cpy #$61		; GRAPHICS?
	bne csgetarie

; ----------------------------------------- func GRAPHICS
; 90ae			original
fngraphics
	ldx #$d0		; =$d000
cga0	ldy #$00
	jmp sxyflp

; ----------------------------------------- 
; 90b5
csgetarie	cpy #$60		; SOUND?
	bne csgetarif

; ----------------------------------------- func SOUND
; 90b9			modified
fnsound	ldx #$d4		; =$d400
	bne cga0

; ----------------------------------------- 
; 90bd
csgetarif	cpy #$67		; NRM?
	bne csgetarig
	jmp fnnrm

; ----------------------------------------- 
; 90c4
csgetarig	cpy #$28		; AT?
	bne csgetarih
	jmp fnat

; ----------------------------------------- 
; 90cb
csgetarih	cpy #$7c		; CHECK?
	bne csgetarii	; no

; ----------------------------------------- func CHECK
; 90cf			recoded
fncheck	jsr $aefa		; sys "("?
	sty helpvc2	; .y=0
	jsr getx8		; get 1st sprite #
	stx helpvec	; store
	lda detectart	; type?
	bne ++

	jsr $aefd		; Spr-Spr: sys chkcom
	jsr getx8		; get 2nd sprite #
	stx helpvec+1	; store
	jsr $aef7		; sys ")"?

; ----------------------------------------- now check

	jsr wdetect	; check: an Kollision beteiligt?
	jmp +

++	jsr $aef7		; Spr-Bgr: sys ")"?
+	ldx helpvec	; and 1st sprite
	jsr wdetect	; check: an Kollision beteiligt?

+	ldy #1		; vorbesetzen: nein (1)
	lda helpvc2	; Abfrage-Ergebnis
	beq ++
	ldx detectart
	bne +
	and #2		; Spr-Spr: beide?
	beq ++		; nein
	
+	dey		; wenn ja  : return 0
++	jmp sxyflp3	; wenn nein: return 1

wdetect	lda detecterg
	and mbittabs,x	; beteiligt?
	beq +		; nein
	inc helpvc2
+	rts		; 70

; ----------------------------------------- 
; 9115
csgetarii	cpy #$7d		; DISPLAY?
	bne csgetarij

; ----------------------------------------- func DISPLAY
; 9119
fndisplay	ldx video		; returns video address
	bne cga0		; normally uncond. branch

; ----------------------------------------- 
; 911e
csgetariend		; end of FN chain

; ----------------------------------------- ERR entry
; 911e			recoded
fnerr	jsr dec7a
	ldy #0
	jsr errnln	; "n" (errn)?	
	beq cerrn

; ----------------------------------------- func ERRLN
; 9128
cerrln	cmp #$4c		; no, "l" (errln)?
	beq +

-	jmp endsmb

; ----------------------------------------- func ERRN 
; 912f
cerrn	ldy errn
	ldx #0
	beq ++

+	jsr errnln	; "n" (errln)?
	bne -

	ldy errln
	ldx errln+1
++	jsr incbasbz
	jmp sxyflp

; ----------------------------------------- 
; 9147
errnln	jsr chrget
	cmp #$4e
	rts

; ----------------------------------------- func D!PEEK
; 914d			new function
csgetarij	cpy #$54		; Token von D!?
	beq fndpeek
	cpy #$7a		; Token von MEM?
	jmp csgetarik

fndpeek	jsr getpeek
	lda (int),y	; get lo
	pha
	iny
	lda (int),y	; get hi
	tax
	pla
	tay
	jmp sxyflp	; .y/.x to float
; 9165
getpeek	ldy #0
	lda #$c2		; PEEK following?
	cmp (progz),y
	beq +
	jmp swarm		; no, bad mode error

+	jsr incbasbz	; yes, advance:
	jsr getint	; sys: "("?, returns .y=0
	jsr sgetadr2	; get address
	jmp $aef7		; check ")"

; ----------------------------------------- 
; 917c
;
; End of part 2
;



