#RetroDevStudio.MetaData.BASIC:2049,Tuned Simons' BASIC,lowercase,10,10
10 REM
20 REM     *** REACTOR 64 ***
30 REM
40 REM     A REAL ACTION-GAME
50 REM
60 REM (C) 1984 BY  LIPPE ENTERPRISES
70 REM     [C.V.D.LIPPE,ASTERNSTR.16]
80 REM     [498 BUENDE 1]
90 REM
91 REM WRITTEN IN  SIMON'S BASIC
92 REM        AND  MACHINE-CODE
93 REM             FOR THE CBM-64
94 REM" TRANSCODED TO  BY . ETTKE
95 :

# BASIC SPEICHER RESERVIEREN FUER MC-ROUTINE
96 POKE56,122:CLR:CLS:CSET1:COLOR0,0,15:CENTER"EACTOR "
98 IFD!PEEK($7A00)<>169THENSCRLDDEF$7A,6,0,3:SCRLD1,8,3,"MC-REACTOR":SCRLDRESTORE

# VARIABLEN INITIALISIEREN
110 CHECK:GA=$7A00:CB=$E000
111 R1$=DUP(" ",9):A=0*LEN(R1$)+D!PEEK(71):A=D!PEEK(A+1)
112 RESET41000:FORI=0TO8:READX$:POKEA+I,NRM(X$):NEXT
120 .RESET:POKE198,0:HI=0:SL=0:SC=0

# ENDLESS GAME LOOP
1000 LOOP
1010 SPRITES:VORSPANN:.RESET
1020 REPEAT:Z=0
1030 POKE198,0:SCREEN:PAUSE37:UNFLASH:A=PEEK(197):IFA=64THENZ=1
1110 IFA=10THENSPIELEN:Z=1
1120 IFA=28THENANLEITUNG
1135 IFA=20THENSELECT
1137 IFA=18THENREALLY:IFX>0THENCLS:CENTERAT(12,0)"YE BYE!":PAUSE5:COLD
1140 IFZ=0THEN1030
1150 UNTILZ=1:COLOR0,0:CLS
1160 END LOOP

9000 END

10000 PROC SPIELEN
10010 LV=SL:SP=5:SH=1
# SPIELSCHLEIFE
10030 REPEAT
10035 IFLV>7THENPR=1:ELSE PR=0
10040 LEVEL:ZEICHENSATZ:SPRITES:BILDAUFBAU
10060 .SOUND2:.SOUND3:.SOUND4
10090 SYSGA:.RESET:E=PEEK(2):IFE=8THENPOINTS:LV=LV+1:SP=6
10120 SP=SP-1
10130 UNTILE=1ORSP=0:SL=LV
# SPIEL BEENDET, GRUENDE:
10140 IFE=1THENDO
# ZEIT ABGELAUFEN (E=1)
10150 COLOR,7:PRINTAT(4,7)"CHINA SYNDROME!":PRINTAT(6,8)"YOU HAVE LOST"
10160 PAUSE3:COLOR,0:SC=0
10170 DONE
# LEVEL NICHT GESCHAFFT (SP=0)
11000 .RESET:MOB SET0,11,7,0,0,3:FILL1,1,28,23,32,0
11005 MMOB0,234,185
11010 FORX=128TO0STEP-1:GETX$:I=JOY(2)
11020 RLOCMOB0,120+COS(X/5)*X,128+SIN(X/5)*X
11030 IFIORX$>""THENX=0
11040 NEXTX
# HIGHSCORE ERMITTELN UND GGF. ANZEIGEN
11060 IFSC>HITHENDO
11070 COLOR,11:PRINTAT(20,11)"HIGHSCORE"
11080 FLASH11,3:.SOUND2:SOUND3,$489C:WAVE3,$41
11120 FORX=0TO255:VOLXAND15:NEXT:UNFLASH:HI=SC:WAVE3,0
11190 DONE
11200 PAUSE3:.RESET
11210 END PROC

12000 PROC POINTS
12010 .SOUND2:Z=PEEK($03CE):WAVE3,33
12030 FORX=ZTO159:SC=SC+1:SOUND3,((160-X)/2)*256:POKECB+800+X,0
12040 COLOR,15:USEAT(19,32)" #####",SC;
12050 NEXT:Z=PEEK($03CF)
12070 FORX=ZTO103:SC=SC+1:SOUND3,((103-X)/1.5)*256:POKECB+192*8+X,0
12080 COLOR,15:USEAT(19,32)" #####",SC;
12090 NEXT:WAVE3,0
12100 COLOR,11:PRINTAT(6,6)"SCORE=SCORE+LV*100":FLASH11,12
12120 IFLV>0THENDO
12130 FORX=1TOLV
12140 WAVE3,17:SOUND3,$3400:PAUSE1:WAVE3,0:SC=SC+100
12160 COLOR,15:USEAT(19,32)" #####",SC;
12170 PAUSE1:NEXT:UNFLASH:PAUSE1
12175 DONE
12180 PAUSE2:UNFLASH:IFSH*2000>SCTHENEND PROC
12200 IFSP>5THENEND PROC
12210 SH=SH+1:SP=SP+1:COLOR,1:PRINTAT(17,9)"ONUS  ONDE":WAVE3,65
12230 FORX=0TO100:SOUND3,X*256
12240 INV17,9,12,1:NEXT:WAVE3,0:PAUSE1
12250 END PROC

15000 PROC SELECT
15010 COLOR8,8,0:CLS:B$="ELECT EVEL USING OYSTICK OR EYBOARD."
15015 PRINTAT(1,0)B$;AT(23,0)B$;
15020 REPEAT
15025 COLOR,0:PRINTAT(12,15)"EVEL: ";:COLOR,7:USE"#",SL
15030 GETB$:JO=JOY(2):REPEAT:UNTILJOY(2)=0
15040 IFB$>"/"THENIFB$<":"THENSL=VAL(B$)
15050 IFJO=1THENSL=SL+1:IFSL>9THENSL=0
15060 IFJO=5THENSL=SL-1:IFSL<0THENSL=9
15070 UNTILB$=CHR$(13)OR(JO>127)
15080 FLASH7,6:PAUSE1:UNFLASH:CLS
15090 END PROC

20000 PROC ANLEITUNG
20010 COLOR8,8,0:CLS:CSET1:CENTER"***   64 ***":PRINT:PRINT
20020 PRINT"IN TOMREAKTOR IST KURZ VOR DEM URCH-":PRINT
20030 PRINT"GEHEN. S GIBT NUR EINE EINZIGE HANCE":PRINT
20040 PRINT"IHN DAVOR ZU BEWAHREN! ER USWEG BE-":PRINT
20050 PRINT"STEHT DARIN, DIE AENDE DES EAKTORKERNS"
20060 PRINT"SO MIT NERGIE ZU VERSORGEN, DASS SIE IN"
20070 PRINT"DER AGE SIND, DEN EAKTORKERN ZU STABI-"
20080 PRINT"LISIEREN. IESES IST EINE SEHR PROBLE-":PRINT
20090 PRINT"MATISCHE NGELEGENHEIT UND DESHALB HAT":PRINT
20100 PRINT"MAN ICH, DEN GROESSTEN LEBENDEN TOM-":PRINT
20110 PRINT"PEZIALISTEN ZU ILFE GEHOLT.":TASTE:CLS

20120 PRINT"IR STEHEN FUENF ONDEN ZUR ERFUEGUNG.":PRINT
20130 PRINT"U MUSST SIE ERST MIT NERGIE BETANKEN,":PRINT
20140 PRINT"DIE ES BEIM OWER-EICHEN GIBT. N-":PRINT
20150 PRINT"SCHLIESSEND MUSST U MIT DER ONDE EINE":PRINT
20160 PRINT"AND DES EAKTORKERNS  BERUEH-"
20170 PRINT"REN. ERFUEGT DIE AND UEBER GENUG":PRINT
20180 PRINT"NERGIE, VERFAERBT SIE SICH WEISS.":PRINT
20190 PRINT"IND ALLE VIER EAKTORKERNWAENDE WEISS,":PRINT
20200 PRINT"HAST U ES MAL WIEDER GESCHAFFT.":TASTE:CLS

20210 PRINT"IES HOERT SICH RECHT LEICHT AN, JEDOCH":PRINT
20220 PRINT"EXPLODIERT DIE ONDE IMMER, FALLS SIE":PRINT
20230 PRINT"ETWAS BERUEHRT, DA SIE SEHR EMPFINDLICH":PRINT
20240 PRINT"IST. USSERDEM SCHWIRREN IM ERN IMMER":PRINT
20250 PRINT"EINIGE EUTRONEN HERUM, DIE ICH BE-":PRINT
20260 PRINT"HINDERN, UND AB DEM ACHTEN EVEL TAUCHT":PRINT
20270 PRINT"SOGAR EIN ROTON AUF.":PRINT
20280 PRINT"AST U ES ABER DANN TROTZDEM GESCHAFFT,"
20290 PRINT"WIRST U ZU EINEM NEUEN RISENHERD":PRINT
20300 PRINT"GERUFEN, DER NOCH SCHWIERIGER IST":PRINT
20310 PRINT"(HOEHERER EVEL).":TASTE:CLS

20320 PRINT"LS ILFE FUER ICH GIBT ES DIE TASIS-":PRINT
20330 PRINT"NERGIE. RUECKST U AUF DEN EUERKNOPF,"
20340 PRINT"HAELT AUF DEM ILDSCHIRM ALLES AN,":PRINT
20350 PRINT"NUR U SELBST KANNST ICH NOCH BEWEGEN.":PRINT
20360 PRINT"EIDER DAUERT DIESER USTAND MAXIMAL":PRINT
20370 PRINT"NUR EINIGE EKUNDEN, DANN IST DIE":PRINT
20380 PRINT"TASIS-NERGIE VERBRAUCHT. UNKTE":PRINT
20390 PRINT"GIBT ES NACH JEDEM UEBERSTANDENEN EVEL:"
20400 PRINT
20410 PRINT"- UNKTE FUER UEBRIG GEBLIEBENE EIT":PRINT
20420 PRINT"- UNKTE FUER UEBRIG GEBLIEBENE TASIS-"
20430 PRINT"  NERGIE":PRINT
20440 PRINT"- UNKTE FUER DEN ENTSPRECHENDEN EVEL":PRINT:TASTE:CLS

20450 PRINT"LLE 2000 UNKTE GIBT ES AUSSERDEM NOCH":PRINT
20460 PRINT"EINE USATZSONDE, DIE U SICHERLICH":PRINT
20470 PRINT"DRINGEND BENOETIGEN WIRST.":PRINT
20480 PRINT"ESTEUERT WIRD MIT DEM OYSTICK IN":PRINT
20490 PRINT"ORT 2.":PRINT
20500 PRINT"EM KEIN OYSTICK ZUR ERFUEGUNG STEHT,":PRINT
20510 PRINT"KANN ES JA MAL MIT DEN ASTEN [],":PRINT
20520 PRINT"[1],[2],[] UND [] VERSUCHEN.":PRINT:PRINT:PRINT
20550 PRINT"IEL LUECK!!!":TASTE
20560 CLS:COLOR0,0,15:CENTER"AIT..."
20570 END PROC

29000 PROC TASTE
29010 CENTERAT(24,0)"> RESS ANY KEY <"
29020 REPEAT:GETB$:I=JOY(2):UNTILIORB$>""
29030 END PROC

30000 PROC .SOUND1
30020 ENVELOPE3,0,0,4,0
30030 SOUND4,$1E00:POKESOUND+23,243:VOL21
30050 SOUND3,$0200
30060 WAVE3,$41,$0400
30090 END PROC

31000 PROC .SOUND2
31010 ENVELOPE3,0,0,14,0
31020 WAVE3,65,$0500:VOL5
31030 SOUND3,$3427
31070 END PROC

32000 PROC .SOUND3
32010 ENVELOPE2,4,4,0,0
32015 WAVE2,0
32020 SOUND2,$684E
32040 VOL5
32090 END PROC

33000 PROC .SOUND4
33010 ENVELOPE1,0,13,0,15
33020 WAVE1,0
33030 SOUND1,$0F00
33050 END PROC

40000 PROC BILDAUFBAU
40010 COLOR0,0
# UMRANDUNG
40040 INSERTR1$,0,0,30,25,5
40110 MOB SET1,1,7,0,0,3:MMOB1,120,127
# REAKTORKERN
40125 FCOL1,1,28,23,7
# REAKTORKERNWAND
40130 Y=8:FORX=14TO11STEP-1:FILLY,X,1,1,90,14:Y=Y+1:NEXT
40140 FORX=11TO14:FILLY,X,1,1,91,14:Y=Y+1:NEXT
40150 Y=8:FORX=15TO18:FILLY,X,1,1,92,14:Y=Y+1:NEXT
40160 FORX=18TO15STEP-1:FILLY,X,1,1,93,14:Y=Y+1:NEXT

# SPRITES FUER EXPLOSION VORBEREITEN
40260 FORX=4TO7
40270 MOB SETX,10,1,0,0,0,0
40280 MMOBX,0,0
40290 NEXT
# ENERGIE-DEPOT
40310 FCHR2,2,2,2,88
40320 MOB SET 2,4,4,0,0
40330 MMOB2,40,64
# BASIS+SONDE
40410 FCHR21,26,2,2,96
40420 MOB SET0,0,10,0,0,0
40430 MMOB0,232,216
# LEVEL SPEZIFISCHES BILD
40505 GOSUB40510:GOTO40600
40510 CGOTO(LVAND7)*100+42000
# TIME-SAEULE INITIALISIEREN
40600 FCOL4,31,1,20,2
40610 FCOL4,31,1,17,8
40620 FORX=0TO19
40630 FCHR4+X,31,1,1,X+100
40640 NEXT
# RECHTE BILDSCHIRMSEITE FUELLEN
40710 COLOR,7:PRINT AT(1,30)"REACTOR 64"
40720 COLOR,9:PRINTAT(3,36)"@1984BY";
40725 COLOR,4:PRINTAT(6,38)"LIPPE";
40730 PRINT"ENTERPRISES"
40740 COLOR,10:PRINTAT(24,30)"TIME";
40750 COLOR,7:PRINTAT(4,34)"STASIS-ENERGY"
40760 COLOR,15:PRINTAT(18,33)"SCORE"
40770 USEAT(19,32)" #####",SC
40780 PRINTAT(21,33)"LEVEL"
40790 USEAT(22,32)" #####",LV;
40795 IFSP>1THENFILL24,35,SP-1,1,99,10
# STASIS-SAEULE INIT.
40810 FCOL4,33,1,13,5
40820 FORX=0TO12
40830 FCHRX+4,33,1,1,X+192
40840 NEXT
# PROTON INIT.
40855 POKE$03D0,0
40860 IFPR<>0THENDO
40865 POKE$03D0,1
40870 MOB SET3,5,15,0,0
40880 MMOB3,128,135
40885 DONE
# NEUTRONEN
40905 E=(LVAND7)/4+2
40906 IFLV>15THENE=E+(LVAND240)/16*2
40910 FORZZ=1TOE
40920 REPEAT:X=INT(RND(1)*30)
40930 Y=INT(RND(1)*25):A=$CC00+X+Y*40
40940 UNTILPEEK(A)=32
40950 FCHRY,X,1,1,RND(1)*8+64
40960 NEXTZZ
40970 END PROC

# BOX CHARS
41000 DATA D1,D0,D7,D2,20,D6,D3,D4,D5

# LEVEL 0
42000 FCHR12,1,1,1,96
42020 FCHR1,22,1,1,96
42050 RETURN

# LEVEL 1
42100 FILL2,14,1,6,89,3
42120 FCHR1,22,1,1,96
42150 RETURN

# LEVEL 2
42200 FORX=0TO1
42210 FILL10+X*7,X*13+3,1,1,94,3
42220 FILL10+X*7,X*13+4,1,1,95,3
42230 FILL10+X*7,X*13+5,1,1,97,3
42240 FILL10+X*7,X*13+6,1,1,98,3
42250 NEXT
42290 RETURN

# LEVEL 3
42300 COLOR,7:PRINTAT(4,6)"WHO THE HELL ARE YOU?"
42320 FILL20,4,1,1,89,3
42330 FILL18,2,1,1,89,3
42340 FCHR3,1,1,1,96
42390 RETURN

# LEVEL 4
42400 J=3:FORI=21TO14STEP-1
42420 FILLI,J,1,1,89,3
42430 NEXT
42440 FILL5,1,5,1,89,10
42450 FILL6,16,1,1,89,10
42490 RETURN

# LEVEL 5
42500 COLOR,2:PRINTAT(7,6)"HALLELUJA!"
42520 FCHR22,18,1,1,96
42590 RETURN

# LEVEL 6
42600 FILL2,25,1,1,94,3
42620 FILL2,26,1,1,95,3
42630 FILL2,27,1,1,97,3
42640 FILL2,28,1,1,98,3:PRINTAT(18,5)"DER CBM-64 IST SUPER"
42650 FCHR1,1,3,3,96:FCHR4,20,1,1,96:FCHR2,2,2,2,88
42690 RETURN

# LEVEL 7
42700 FILL1,15,1,4,89,5:FILL12,1,5,1,89,5
42720 FILL11,24,5,1,89,5:FILL20,14,1,4,89,5
42790 RETURN

45000 PROC VORSPANN
45010 NRM:CLS:HIRES0,0:MULTI1,7,128
45015 CSET1:.LOGO
45020 TEXT18,10,"REACTOR 64",3,6,12
45030 TEXT16,8,"REACTOR 64",2,6,12
45060 CIRCLE73,108,50,24,1
45070 CIRCLE73,108,14,60,1
# THIS PAINTS TWO ELLIPSES INTO THE GFX:
45090 FORX=0TO331*8STEP331*8
45100 DESIGN3,CB+449*8+X
45110 @....
45120 @.CC.
45130 @CCCC
45140 @CCCC
45150 @CCCC
45160 @.CC.
45170 @....
45180 @....
45190 NEXT
45200 TEXT5,182,"  1984 BY IPPE NTERPRISES",3,1,5
45210 CIRCLE8,185,6,7,3
45220 COLOR2,2:CSET2:MULTI1,7,128
45230 MOB SET1,17,13,0,0,3
45240 MMOB1,146,138
45250 Y=3:.SOUND1
45260 FORX=1TO350
45265 GETX$:I=JOY(2)
45270 MOB SET1,16+Y,XAND15,0,0:Y=Y-1:IFY=0THENY=3
45280 SOUND3,RND(1)*256:XX=PEEK(197):IFXX<>64THENX=350
45285 IFIORX$>""THENX=350
45290 NEXTX:MOBOFF1:COLOR0,0:CLS:HIRES0,0:MULTIOFF
45300 WAVE3,0
45310 END PROC

46000 PROC .LOGO
46050 COLOR0,0:FILL0,0,40,25,105,12
46060 INSERT" ",4,4,32,17,12:COLOR,2
46070 PRINTAT(6,6)""AT(6,23)""
46080 PRINTAT(LIN,6)"  "
46090 PRINTAT(LIN,6)"     "
46100 PRINTAT(LIN,6)"         "
46110 PRINTAT(LIN+1,14)""
46120 PRINTAT(LIN,15)" "
46130 PRINTAT(LIN,15)"   "
46140 PRINTAT(LIN,15)" ":PAUSE1
46150 COLOR,7:CENTERAT(17,0)"BY IPPE NTERPRISES 1984":PRINT:PAUSE1
46160 CENTERAT(18,0)" VERSION BY $ 2025"
46180 END PROC

46200 PROC SCREEN
46210 CLS:WAIT$D011,$80,$80:FLASH11,18:CSET1:COLOR11,15,2:FILL0,0,40,25,105,0
46220 CENTERAT(2,3)" ***  64 *** ",34
46230 INSERT" ",4,3,34,13,0:COLOR,0
46240 PRINTAT(6,8)"ELECT ODE"
46250 PRINTAT(LIN+1,5)"A  AME ODE"
46260 PRINTAT(LIN+1,5)"B  NSTRUCTIONS"
46270 PRINTAT(LIN+1,5)"C  ELECT TARTING EVEL"
46280 PRINTAT(LIN+1,5)"D  ND":FCOL6,8,11,1,11
46290 USEAT(LIN+4,3)" IGHSCORE: #####     EVEL: #### ",HI,SL
46300 FORX=8TO14STEP2:FCOLX,5,1,1,11:NEXT:COLOR,6
46320 CENTERAT(23,0)" @ 1984 BY  IPPE NTERPRISES ":COLOR,0
46330 END PROC

46350 PROC UNFLASH
46360 WAIT$C5C7,$80,$80:FLASHOFF
46370 END PROC

46400 PROC REALLY
46410 INSERT" ",12,10,20,5,2
46420 CENTERAT(14,11)"UIT EACTOR?  Y/N",18
46430 REPEAT:KEYGETX$:UNTILPLACE(X$,"YN")
46440 IFX$="N"THENX=0:ELSEX=1
46450 WAIT$D011,$80,$80
46460 END PROC

47000 PROC LEVEL
47010 HIRES0,0:MULTI2,0,6:COLOR7,7
47020 X$=$$LV
47030 IFLV<10THENX$="0"+RIGHT$(X$,1)
47040 TEXT61,80,"LEVEL",2
47050 TEXT72,96,X$,3,4,10
47060 .SOUND1
47070 SOUND3,$0F00
47075 FORY=1TO0STEP-1
47080 FORX=59TO0STEP-4:XX=PEEK(197):IFXX<>64THENY=0:X=0
47090 REC60-X,70-(X*1.18),40+2*X,60+2.3*X,Y
47100 SOUND3,(X/4*Y-(20-X/4)*(Y-1))*256
47105 GETX$:ZZ=JOY(2):IFZZORX$>""THENX=0:Y=0
47110 NEXTX,Y
47120 .RESET
47130 CLS:CSET0
47500 END PROC

48000 PROC .RESET
48010 FORX=0TO7:MOB OFFX:MMOBX,0,0:NEXT
48020 MEMCLRSOUND,25
48040 END PROC

# --------------------------------------------------
# ZEICHENSATZ AN $E000 UMDEFINIEREN

50000 PROC ZEICHENSATZ
50010 MEM:CSET0

# COPYRIGHT-ZEICHEN
50020 DESIGN2,CB
50030 @.BBBBB..
50040 @B.....B.
50050 @B.BBB.B.
50060 @B.B...B.
50070 @B.B...B.
50080 @B.BBB.B.
50085 @B.....B.
50090 @.BBBBB..

# REAKTORBEGRENZUNG 1
50100 DESIGN2,CB+80*8
50110 @BBBBBBBB
50120 @........
50130 @BBB..BBB
50140 @..B..B..
50150 @..BBBB..
50160 @........
50170 @.B.B.B.B
50180 @BBBBBBBB

# REAKTORBEGRENZUNG 2
50200 DESIGN2,CB+81*8
50210 @BBBBBBBB
50220 @B.......
50230 @B....BBB
50240 @B....B..
50250 @B....B..
50260 @B.BBBB..
50270 @B.B....B
50280 @B.B....B

# REAKTORBEGRENZUNG 3
50300 DESIGN2,CB+82*8
50310 @B.B...BB
50320 @B.B....B
50330 @B.BBB.BB
50340 @B...B..B
50350 @B...B.BB
50360 @B.BBB..B
50370 @B.B...BB
50380 @B.B....B

# REAKTORBEGRENZUNG 4
50400 DESIGN2,CB+83*8
50410 @B.B...BB
50420 @B.B.....
50430 @B.BBBB..
50440 @B....B..
50450 @B....B..
50460 @B....BBB
50470 @B.......
50480 @BBBBBBBB

# REAKTORBEGRENZUNG 5
50500 DESIGN2,CB+84*8
50510 @BBBBBBBB
50520 @B.B.B.B.
50530 @........
50540 @..BBBB..
50550 @..B..B..
50560 @BBB..BBB
50570 @........
50580 @BBBBBBBB

# REAKTORBEGRENZUNG 6
50600 DESIGN2,CB+85*8
50610 @B....B.B
50620 @B....B.B
50630 @..BBBB.B
50640 @..B....B
50650 @..B....B
50660 @BBB....B
50670 @.......B
50680 @BBBBBBBB

# REAKTORBEGRENZUNG 7
50700 DESIGN2,CB+86*8
50710 @B....B.B
50720 @BB...B.B
50730 @B..BBB.B
50740 @BB.B...B
50750 @B..B...B
50760 @BB.BBB.B
50770 @B....B.B
50780 @BB...B.B

# REAKTORBEGRENZUNG 8
50800 DESIGN2,CB+87*8
50810 @BBBBBBBB
50820 @.......B
50830 @BBB....B
50840 @..B....B
50850 @..B....B
50860 @..BBBB.B
50870 @.....B.B
50880 @BB...B.B

# REAKTORWAND 1
51000 DESIGN2,CB+90*8
51010 @......BB
51020 @.....BB.
51030 @....BBB.
51040 @...BB.B.
51050 @..BBB.B.
51060 @.BB..B..
51070 @BBBBB.B.
51080 @B......B

# REAKTORWAND 2
51100 DESIGN2,CB+91*8
51110 @B......B
51120 @BBBBB.B.
51130 @.BB..B..
51140 @..BB..B.
51150 @...BB.B.
51160 @....BBB.
51170 @.....BB.
51180 @......BB

# REAKTORWAND 3
51200 DESIGN2,CB+92*8
51210 @BB......
51220 @.BB.....
51230 @.BBB....
51240 @.B.BB...
51250 @.B..BB..
51260 @..B..BB.
51270 @.B.BBBBB
51280 @B......B

# REAKTORWAND 4
51300 DESIGN2,CB+93*8
51310 @B......B
51320 @.B.BBBBB
51330 @..B..BB.
51340 @.B..BB..
51350 @.B.BB...
51360 @.BBB....
51370 @.BB.....
51380 @BB......

# UNSICHTBARER CHAR UNTER POWER
51400 DESIGN2,CB+88*8
51410 @........
51420 @........
51430 @........
51440 @........
51450 @........
51460 @........
51470 @........
51480 @........

# HINDERNIS
51500 DESIGN2,CB+89*8
51510 @...BB...
51520 @..B..B..
51530 @.B..B.B.
51540 @B.BBB..B
51550 @B..BBB.B
51560 @.B.B..B.
51570 @..B..B..
51580 @...BB...

# TIME SETZEN
51600 FORX=100TO119
51610 DESIGN2,CB+X*8
51620 @BBBBBBBB
51630 @BBBBBBBB
51640 @BBBBBBBB
51650 @BBBBBBBB
51660 @BBBBBBBB
51670 @BBBBBBBB
51680 @BBBBBBBB
51690 @BBBBBBBB
51695 NEXTX

# STASIS SETZ.
51700 FORX=192TO192+12
51705 DESIGN2,CB+X*8
51710 @BBBBBBBB
51720 @BBBBBBBB
51730 @BBBBBBBB
51740 @BBBBBBBB
51750 @BBBBBBBB
51760 @BBBBBBBB
51770 @BBBBBBBB
51780 @BBBBBBBB
51790 NEXTX

# SCHRIFT 'DANGER' 1
51800 DESIGN2,CB+94*8
51810 @........
51820 @.BBBB..B
51830 @..B.B.B.
51840 @..B.B.BB
51850 @..B.B.B.
51860 @.BBBB.B.
51870 @........
51880 @........

# SCHRIFT 'DANGER' 2
51900 DESIGN2,CB+95*8
51910 @........
51920 @..B..B.B
51930 @B.BB.B.B
51940 @B.BBBB.B
51950 @B.B.BB.B
51960 @B.B..B.B
51970 @........
51980 @........

# SCHRIPT 'DANGER' 3
52000 DESIGN2,CB+97*8
52010 @........
52020 @BBB.BBB.
52030 @....B...
52040 @.BB.BB..
52050 @..B.B...
52060 @BBB.BBB.
52070 @........
52080 @........

# SCHRIFT 'DANGER' 4
52100 DESIGN2,CB+98*8
52110 @........
52120 @BBB..B..
52130 @B.B..B..
52140 @BBB..B..
52150 @BB......
52160 @B.B..B..
52170 @........
52180 @........

# SYMBOL FUER ANZAHL SONDEN
52200 DESIGN2,CB+99*8
52210 @...BBB..
52220 @...B.B..
52230 @.BBBBBBB
52240 @.B.B.B.B
52250 @.BBBBBBB
52260 @...B.B..
52270 @...BBB..
52280 @........

# NEUTRON 1
60000 FORX=0TO64STEP64
60010 DESIGN2,CB+64*8+X
60020 @........
60030 @..BBB...
60040 @.BBBBB..
60050 @.BB.BB..
60060 @.BBBBB..
60070 @..BBB.B.
60080 @.....BB.
60090 @.......B

# NEUTRON 2
60100 DESIGN2,CB+65*8+X
60130 @..BBB...
60140 @.BBBBB..
60150 @.BB.BB..
60160 @.BBBBB..
60170 @..BBB...
60180 @..B.B...
60190 @...B....
60195 @...B....

# NEUTRON 3
60200 DESIGN2,CB+66*8+X
60220 @........
60230 @...BBB..
60240 @..BBBBB.
60250 @..BB.BB.
60260 @..BBBBB.
60270 @.B.BBB..
60280 @.BB.....
60290 @B.......

# NEUTRON 4
60300 DESIGN2,CB+67*8+X
60310 @........
60320 @.BBB....
60330 @BBBBBB..
60340 @BB.BB.BB
60350 @BBBBBB..
60360 @.BBB....
60370 @........
60380 @........

# NEUTRON 5
60400 DESIGN2,CB+68*8+X
60410 @........
60420 @....BBB.
60430 @..BBBBBB
60440 @BB.BB.BB
60450 @..BBBBBB
60460 @....BBB.
60470 @........
60480 @........

# NEUTRON 6
60500 DESIGN2,CB+69*8+X
60510 @.......B
60520 @.....BB.
60530 @..BBB.B.
60540 @.BBBBB..
60550 @.BB.BB..
60560 @.BBBBB..
60570 @..BBB...
60580 @........

# NEUTRON 7
60600 DESIGN2,CB+70*8+X
60605 @...B....
60610 @...B....
60620 @..B.B...
60630 @..BBB...
60640 @.BBBBB..
60650 @.BB.BB..
60660 @.BBBBB..
60670 @..BBB...

# NEUTRON 8
60700 DESIGN2,CB+71*8+X
60710 @B.......
60720 @.BB.....
60730 @.B.BBB..
60740 @..BBBBB.
60750 @..BB.BB.
60760 @..BBBBB.
60770 @...BBB..
60780 @........
60790 NEXTX
60795 END PROC

# --------------------------------------------------
# SPRITES DEFINIEREN

60800 PROC SPRITES

# SONDE
60802 DESIGN 0,$C000
60805 @........................
60810 @........................
60820 @.....BBBBBBB............
60830 @.....BBB.BBB............
60840 @.....BB...BB............
60850 @.....B.....B............
60860 @.BBBBBBBBBBBBBBB........
60870 @.BBB.B.....B.BBB........
60880 @.BB..B.BB..B..BB........
60890 @.B...B.B.B.B...B........
60900 @.BB..B..BB.B..BB........
60910 @.BBB.B.....B.BBB........
60920 @.BBBBBBBBBBBBBBB........
60930 @.....B.....B............
60940 @.....BB...BB............
60950 @.....BBB.BBB............
60960 @.....BBBBBBB............
60970 @........................
60980 @........................
60990 @........................
60995 @........................

# REACTORKERN 1
60999 FORX=0TO1024STEP1024
61000 DESIGN 0,$C000+64+X
61010 @........BBBBBBBB........
61020 @......BB........BB......
61030 @.....B...BBBBBB...B.....
61040 @....B...BBBBBBBB...B....
61050 @...B...BB......BB...B...
61060 @...B..BB..BBBB..BB..B...
61070 @..B..BB..BBBBBB..BB..B..
61080 @..B.BB..BB....BB..BB.B..
61090 @..B.BB.BB..BB..BB.BB.B..
61100 @..B.BB.BB.BBBB.BB.BB.B..
61110 @..B.BB.BB.BBBB.BB.BB.B..
61120 @..B.BB.BB..BB..BB.BB.B..
61130 @..B.BB..BB....BB..BB.B..
61140 @..B..BB..BBBBBB..BB..B..
61150 @...B..BB..BBBB..BB..B...
61160 @...B...BB......BB...B...
61170 @....B...BBBBBBBB...B....
61180 @.....B...BBBBBB...B.....
61190 @......BB........BB......
61200 @........BBBBBBBB........
61210 @........................

# REACTORKERN 2
61300 DESIGN 0,$C000+64*2+X
61310 @.........BBBBBB.........
61320 @.......BBB....BBB.......
61330 @......BB........BB......
61340 @.....B...BBBBBB...B.....
61350 @....B...BBBBBBBB...B....
61360 @...BB..BB......BB..BB...
61370 @...B..BB..BBBB..BB..B...
61380 @..BB.BB..BB..BB..BB.BB..
61390 @..B..BB.BB....BB.BB..B..
61400 @..B..BB.B..BB..B.BB..B..
61410 @..B..BB.B..BB..B.BB..B..
61420 @..B..BB.BB....BB.BB..B..
61430 @..BB.BB..BB..BB..BB.BB..
61440 @...B..BB..BBBB..BB..B...
61450 @...BB..BB......BB..BB...
61460 @....B...BBBBBBBB...B....
61470 @.....B...BBBBBB...B.....
61480 @......BB........BB......
61490 @.......BBB....BBB.......
61500 @.........BBBBBB.........
61510 @........................

# REACTOR KERN 3
61600 DESIGN 0,$C000+64*3+X
61610 @........................
61620 @.........BBBBBB.........
61630 @....B..BBBBBBBBBB..B....
61640 @...B..BB........BB..B...
61650 @.....BB..BBBBBB..BB.....
61660 @....BB..BBBBBBBB..BB....
61670 @....B..BB......BB..B....
61680 @...BB.BB..BBBB..BB.BB...
61690 @...BB.BB.BB..BB.BB.BB...
61700 @...BB.BB.B....B.BB.BB...
61710 @...BB.BB.B....B.BB.BB...
61720 @...BB.BB.BB..BB.BB.BB...
61730 @...BB.BB..BBBB..BB.BB...
61740 @....B..BB......BB..B....
61750 @....BB..BBBBBBBB..BB....
61760 @.....BB..BBBBBB..BB.....
61770 @...B..BB........BB..B...
61780 @....B..BBBBBBBBBB..B....
61790 @.........BBBBBB.........
61800 @........................
61810 @........................
61815 NEXTX

# POWER-ZEICHEN
62000 DESIGN 0,$C000+4*64
62010 @........................
62020 @........................
62030 @........................
62040 @.BBB.BBB.B...B..........
62050 @.B.B.B.B.B...B..........
62060 @.BBB.B.B.B.B.B.BB.......
62070 @.B...B.B.B.B.B..........
62080 @.B...BBB.BBBBB.B........
62090 @..............B.........
62100 @.............B..........
62110 @...BBB.BBB..B...........
62120 @...B...B.B.BBBBBB.......
62130 @...BB..BBB.....B........
62140 @...B...BB.....B.........
62150 @...BBB.B.B...B..........
62160 @............B...........
62170 @........................
62180 @........................
62190 @........................
62200 @........................
62210 @........................

# PROTON 1
62300 DESIGN0,$C000+5*64
62310 @......BBBB..............
62320 @........................
62330 @........................
62340 @......BBBB..............
62350 @.....B.BB.B.............
62360 @....BB.BB.BB............
62370 @B..B...BB...B..B........
62380 @B..BBBBBBBBBB..B........
62390 @B..BBBBBBBBBB..B........
62400 @B..B...BB...B..B........
62410 @....BB.BB.BB............
62420 @.....B.BB.B.............
62430 @......BBBB..............
62440 @........................
62450 @........................
62460 @......BBBB..............
62470 @........................
62480 @........................
62490 @........................
62500 @........................
62510 @........................

# PROTON 2
62600 DESIGN0,$C000+6*64
62610 @..........BB............
62620 @............BB..........
62630 @.B......................
62640 @.B....BBBB..............
62650 @B....BB..BB.............
62660 @B...BBB..BBB............
62670 @...BBBB..BBBB...........
62680 @...B...BB...B...........
62690 @...B...BB...B...........
62700 @...BBBB..BBBB...........
62710 @....BBB..BBB...B........
62720 @.....BB..BB....B........
62730 @......BBBB....B.........
62740 @..............B.........
62750 @..BB....................
62760 @....BB..................
62770 @........................
62780 @........................
62790 @........................
62800 @........................
62810 @........................

# PROTON 3
63000 DESIGN0,$C000+7*64
63010 @....BB..................
63020 @..BB....................
63030 @..............B.........
63040 @..............B.........
63050 @......BBBB.....B........
63060 @.....BBBBBB....B........
63070 @....BB.BB.BB............
63080 @....BBB..BBB............
63090 @....BBB..BBB............
63100 @....BB.BB.BB............
63110 @B....BBBBBB.............
63120 @B.....BBBB..............
63130 @.B......................
63140 @.B......................
63150 @............BB..........
63160 @..........BB............
63170 @........................
63180 @........................
63190 @........................
63200 @........................
63210 @........................

# EXPLOSION 1
63300 DESIGN0,$C000+8*64
63301 @.....BB.B...............
63302 @....B...B.B.B...........
63303 @...B.BB.BB.B.BB.........
63304 @..BB.B...BB...B.........
63305 @.BBBB..BB..BBB.B........
63306 @.B...BB...BB...B........
63307 @B...BB.BB..BBB..........
63308 @.BB..BBB..B...B.........
63309 @...B.B.....BB...........
63310 @BB.BB.BBBBBB.BB.........
63311 @..B..BBB.....BB.........
63312 @...B....B...B.B.........
63313 @.BBB..BBBB.BB..B........
63314 @...BBB...B..BBB.........
63315 @..BB.B.B.B.BB...........
63316 @......BBB..B............
63317 @........................
63318 @........................
63319 @........................
63320 @........................
63321 @........................

# EXPLOSION 2
63350 DESIGN0,$C000+9*64
63351 @........................
63352 @.....BB...BB............
63353 @...B.....BB.............
63354 @..BB.BB.....BB..........
63355 @.......BB.BB.B..........
63356 @.B.BB...................
63357 @BB...BBB..BB............
63358 @...B......B..B..........
63359 @..BB.BB.BBB.BB..........
63360 @.B......................
63361 @..BB.B.....BB...........
63362 @B.B..B.BB...............
63363 @BB.......B....B.........
63364 @..BB...B..BB.B..........
63365 @.....B.BB...............
63366 @.....BB...B.............
63367 @........................
63368 @........................
63369 @........................
63370 @........................
63371 @........................

# EXPLOSION 3
63400 DESIGN0,$C000+10*64
63401 @........................
63402 @........................
63403 @......BB....BB..........
63404 @......BB....BB..........
63405 @..BB.......B............
63406 @..BB....................
63407 @......BBB...............
63408 @......BB...B............
63409 @BB........BB............
63410 @BB..BB....B...B.........
63411 @....BB.......BB.........
63412 @........................
63413 @......BB..BB............
63414 @..B...BB..BB............
63415 @..BBB......B............
63416 @........................
63417 @........................
63418 @........................
63419 @........................
63420 @........................
63421 @........................

# SCHRIFT 'GAME OVER'
63500 DESIGN0,$C000+11*64
63501 @.BBB....B...BBB.BBB.BBBB
63502 @B...B..B.B..B.B.B.B.B...
63503 @B......B.B..B.BBB.B.BBB.
63504 @B.BBB.BBBBB.B.....B.B...
63505 @B...B.B...B.B.....B.B...
63506 @.BBB..B...B.B.....B.BBBB
63507 @........................
63509 @........................
63510 @.BBB..B...B.BBBB.BBB...B
63511 @B...B.B...B.B....B..B..B
63512 @B...B.B...B.B....B..B..B
63513 @B...B..B.B..BBB..BBB...B
63514 @B...B..B.B..B....BB....B
63515 @B...B..B.B..B....B.B....
63516 @.BBB....B...BBBB.B..B..B
63517 @........................
63518 @.BBB.BBB.BBB.BBB.B.B..B.
63519 @.B...B.B.B.B.B.B.B.B..B.
63520 @.BBB.B.B.BBB.BBB.BBB..B.
63521 @...B.B.B.BB..BB...B.....
63522 @.BBB.BBB.B.B.B.B..B...B.

63523 END PROC

